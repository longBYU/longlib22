C
C *** LAST REVISED ON  4-JAN-1994 10:14:54.86
C *** SOURCE FILE: [LONGLIB93.SOURCES.FORTRAN.MASTER]BARCHR.FOR
C
	SUBROUTINE BARCHR(VALS,NB,NS,ISH,IFLAG,XLEN,YLEN,VMIN,VMAX,
     $			FORM,NDIV,SP,SLAB,NSLAB,BLAB,NBLAB,CS,
     $			T,NT,BT,NBT,LT,NLT,TCS,ALEG,DIS,IWT,ICOL)
C
C	ROUTINE TO PLOT A BAR CHART WITH SHADED BARS OR SHADED REGIONS
C	BETWEEN LINES WITH DESCRIPTIVE LEGENDS
C
C	WRITTEN:  DGL  AUG 1987
C
C	VALS	(R) VALUE FOR EACH BAR SEGMENT DIMENSIONED VALS(NB,NS)
C		    WHERE NB IS THE NUMBER OF BARS AND NS IS THE
C		    NUMBER OF SEGMENTS IN EACH BAR
C	NB	(I) NUMBER OF BARS/NUMBER OF POINTS (25>NB>0)
C	NS	(I) NUMBER OF SEGMENTS IN EACH BAR/NUMBER OF LINES (10>NS>0)
C	ISH	(R) SHADE OPTION FOR EACH SEGMENT DIMENSIONED ISH(NS)
C			VALUE OF ISH   SHADE PATTERN
C			    0		 NO SHADING (OUTLINE ONLY)
C			    1		 -45 DEG SOLID LINES
C			    2		 HORIZONTAL SOLID LINES
C			    3		 +45 DEG SOLID LINES
C			    4		 VERTICAL SOLID LINES
C			    5		 -45 DEG DOTTED LINES
C			    6		 HORIZONTAL DOTTED LINES
C			    7		 +45 DEG DOTTED LINES
C			    8		 VERTICAL DOTTED LINES
C			    9		 +/- 45 DEG DOTTED LINES
C			   10		 HORIZONTAL/VERTICAL DOTTED LINES
C			   11		 +/- 45 DEG SOLID LINES
C			   12		 HORIZONTAL/VERTICAL SOLID LINES
C	IFLAG	(R) PLOTTING FLAG
C			< 0 DON'T CLOSE PLOT PACKAGE
C			> 0 CLOSE PLOT PACKAGE
C		MAG	> 10000 INIT PLOT PACKAGE
C	(ONE'S DIGIT)   = 1 DON'T USE COLORS
C		        = 2 USE COLORS
C	(TEN'S DIGIT)	= 0 VERTICAL BAR CHART
C			= 1 HORIZONTAL BAR CHART
C			= 2 LINE CHART WITH SHADING BETWEEN LINES
C	(HUNDRED'S)	= SCREEN GRAPHICS DEVICE (SEE FRAME)
C	XLEN	(R) HEIGHT OF CHART
C		    IF XLEN <0 THEN CHART IS PUT IN BOX
C	YLEN	(R) WIDTH OF CHART
C	VMIN	(R) MINIMUM VALUE OF VALS SHOWN ON CHART
C	VMAX	(R) MAXIMUM VALUE OF VALS SHOWN ON CHART
C		    NOTE: IF VMIN=VMAX THEN AUTO SCALING IS USED.
C	FORM	(R) NUMERIC LABEL FORMAT (SEE NUMBER)
C	NDIV	(I) NUMBER OF DIVSIONS ALONG BAR/LINE LENGTH
C		    < 0 LINES ACROSS BAR SPACE SHOWN
C		    = 0 NO DIVISION OR NUMERIC LABELS
C		    > 0 NO LINES ACROSS BAR SPACE SHOWN
C	SP	(R) WIDTH OF BAR
C		    = 0 BARS EVENLY SPACE WITH AUTO SPACING
C		   <> 0 BARS GROUPED IN GROUPS OF INT(SP), WITH SPACING
C			BETWEEN BARS IN GROUPS SPECIFIED BY FRAC(SP)
C	SLAB	(C) SEGMENT LABEL ARRAY DIMENSIONED SLAB(NS)
C	NSLAB	(I) MAXIMUM NUMBER OF CHARACTERS IN SEGMENT LABELS
C	BLAB	(C) BASE LABEL ARRAY DIMENSIONED BLAB(NB)
C	NBLAB	(I) MAXIMUM NUMBER OF CHARACTERS IN BASE LABELS
C	CS	(R) LABEL CHARACTER SIZE
C	T	(C) TITLE ABOVE CHART
C	NT	(I) NUMBER OF CHARACTERS IN TITLE
C	BT	(C) TITLE BESIDE BASE OF BARS
C	NBT	(I) NUMBER OF CHARACTERS IN BT
C	LT	(C) TITLE ALONG SIZE OF BARS
C	NLT	(B) NUMBER OF CHARACTERS IN NLT
C	TCS	(R) TITLE CHARACTER SIZE
C	ALEG	(R) LEGEND LOCATION
C		    ALEG(1)= LEGEND SHADING BOX SIZE 
C			      = 0 NO LEGEND
C			      < 0 LEGEND PLOTTED TO RIGHT OF CHART 
C				  AND ALEG(2),ALEG(3) NOT USED
C			      > 0 ALEG(2),ALEG(3) USED
C		    ALEG(2)= X POSITION OF LOWER LEFT CORNER OF LEGEND
C		    ALEG(3)= Y POSITION OF LOWER LEFT CORNER OF LEGEND
C	DIS	(R) SHADING LINE DISTANCE
C		    > 0 LINE WEIGHTS NOT USED
C		    < 0 LINE WEIGHTS USED
C	IWT	(I) PEN WIDTH ARRAY (0-9)
C		    IWT(1) = AXIS LINE WIDTH
C		    IWT(2) = DIVISION LINE WIDTH
C		    IWT(3) = BAR OUTLINE/LINE WIDTH
C		    IWT(4) = LABEL WIDTH
C		    IWT(5) = TITLE WIDTH (BT,LT)
C		    IWT(6) = TOP TITLE WIDTH
C	ICOL	(I) COLOR ARRAY
C		    ICOL(1) = TOP OF CHART TITLE COLOR (RETURN)
C		    ICOL(2) = BT TITLE COLOR
C		    ICOL(3) = LT TITLE COLOR
C		    ICOL(4) = AXIS TITLE COLOR
C		    ICOL(5) = SLAB COLOR
C		    ICOL(6) = BLAB COLOR
C		    ICOL(7) = SEGMENT COLOR 1
C		    ICOL(8) = SEGMENT COLOR 2
C		     ...          ...
C
	DIMENSION VALS(NB,NS),ISH(NS),ICOL(*),IWT(*),ALEG(*)
	CHARACTER*(*) T,BT,LT
	CHARACTER*(*) SLAB(*),BLAB(*)
C
	PARAMETER (NWORK=1000)	! WORK SPACE FOR SHADING
	DIMENSION W(NWORK)
C
	LOGICAL REPEAT
	DATA REPEAT/.FALSE./
	KK=0
C
C	INTIALIZE ROUTINE AND PLOT PACKAGE IF REQUIRED
C
	IF (IFLAG.EQ.0) THEN
		IF (REPEAT) CALL PLOTND	!CLOSE PLOTTER
		REPEAT=.FALSE.
		RETURN
	ENDIF
C
C	INTIALIZE PLOT PACKAGE
C
	JF=IABS(IFLAG)
	IF (.NOT.REPEAT.AND.JF.LT.10000) THEN
		ILU=JF/100			!GRAPHICS DEVICE CODE
		CALL FRAME(3,ILU,1.5,1.0,1.)	!INTIALIZE
	ELSE
		CALL CTERM(-1)			!PUT TERMINAL IN PLOT MODE
	ENDIF
	JFF=MOD(JF,100)/10			!OPTION FLAG
	JF=MOD(JF,10)				!COLOR FLAG
C
	VM=VMIN
	VX=VMAX
	SCALE=VX-VM
	IF (SCALE.LE.0.0) THEN
		VM=VALS(1,1)
		VX=VALS(1,1)
		DO 10 I=1,NB
			DO 10 J=1,NS
				VM=MIN(VALS(I,J),VM)
				VX=MAX(VALS(I,J),VX)
10		CONTINUE
		SCALE=VX-VM
		IF (SCALE.EQ.0.0) SCALE=1.0
	ENDIF
C
C	MAKE CHART AXISES
C
	IF (DIS.LT.0.0) CALL NEWPEN(IWT(1)*10)
	IF (JF.EQ.2) CALL PLOT(FLOAT(ICOL(4)),0.,0)
	CALL PLOT(0.,YLEN,3)
	CALL PLOT(0.,0.,2)
	CALL PLOT(ABS(XLEN),0.,2)
	IF (XLEN.LT.0.0) THEN
		CALL PLOT(ABS(XLEN),YLEN,2)
		CALL PLOT(0.,YLEN,2)
	ENDIF
C
C	MAKE AXIS LABELS
C
	ND=IABS(NDIV)
	AM=0.0
	IF (ND.EQ.0) GOTO 50
	IF (DIS.LT.0.0) CALL NEWPEN(IWT(4)*10)
	SCALE=SCALE/FLOAT(ND)
	IF (JFF.NE.1) THEN
C
C	COMPUTE NUMERIC LABEL LENGTHS
C
		DO 15 I=0,ND
			AV=VM+SCALE*I
			X1=0.
			Y1=0.
			CALL NUMBER(X1,Y1,CS,AV,0.,FORM,-3)
			AM=MAX(AM,X1)
15		CONTINUE
C
C	DISPLAY NUMERIC LABELS ON Y AXIS
C
		DO 20 I=0,ND
			AV=VM+SCALE*I
			X1=-AM-CS/2.
			Y1=I*YLEN/ND
			CALL NUMBER(X1,Y1,CS,AV,0.,FORM,-1)
20		CONTINUE
		IF (NDIV.LT.0) THEN
			IF (DIS.LT.0.0) CALL NEWPEN(10)
			DO 25 I=1,ND
				Y1=I*YLEN/ND
				CALL PLOT(0.,Y1,3)
				CALL PLOT(ABS(XLEN),Y1,2)
25			CONTINUE
		ENDIF
	ELSE
C
C	DISPLAY NUMERIC LABELS ON X AXIS
C
		DO 30 I=0,ND
			AV=VM+SCALE*I
			Y1=-CS
			X1=I*ABS(XLEN)/ND
			CALL NUMBER(X1,Y1,CS,AV,0.,FORM,0)
30		CONTINUE
		IF (NDIV.LT.0) THEN
			IF (DIS.LT.0.0) CALL NEWPEN(10)
			DO 35 I=1,ND
				X1=I*ABS(XLEN)/ND
				CALL PLOT(X1,0.,3)
				CALL PLOT(X1,YLEN,2)
35			CONTINUE
		ENDIF
	ENDIF
C
C	MAKE CHART BARS/LINES
C
50	CONTINUE
	IF (YLEN.EQ.0.0.OR.XLEN.EQ.0.0) GOTO 200
	IF (JFF.EQ.1) THEN
		SCALE=ABS(XLEN)/SCALE/FLOAT(ND)
	ELSE
		SCALE=YLEN/SCALE/FLOAT(ND)
	ENDIF
C
	IF (JFF.LT.2) THEN
C
C	MAKE BAR CHART
C
	    VSP=ABS(XLEN)/FLOAT(NB)		! VERTICAL BARS
	    IF (JFF.EQ.1) VSP=YLEN/FLOAT(NB)	! HORIZONTAL BARS
	    WB=(ABS(SP)-INT(ABS(SP)))/2.
	    KK=INT(ABS(SP))
	    IF (KK.EQ.1) KK=0
	    IF (WB.EQ.0.0) WB=VSP*0.4		! AUTO SCALE
	    DO 100 I=1,NB
		DO 75 J=1,NS
			IF (JF.EQ.2) CALL PLOT(FLOAT(ICOL(J+6)),0.,0)
C
C	DO AREA OUTLINE
C
			IF (JFF.EQ.1) THEN	! HORIZONTAL BARS
				Y1=(I-0.5)*VSP
				IF (KK.NE.0) THEN
				    K=MOD(I-1,KK)
				    Y1=Y1+(VSP-WB)*
     $					(FLOAT(K)-FLOAT(KK-1)/2.)
				ENDIF
				YY=WB
				IF (J.GT.1) THEN
					AV=0.0
					DO 112 K=1,J-1
						AV=AV+VALS(I,K)
112					CONTINUE
					X1=(AV-VM)*SCALE
					AV=AV+VALS(I,J)
					XX=(AV-VM)*SCALE
				ELSE
					X1=0.0
					XX=(VALS(I,J)-VM)*SCALE
				ENDIF
				W(1)=X1
				W(2)=Y1-YY
				W(3)=XX
				W(4)=Y1-YY
				W(5)=XX
				W(6)=Y1+YY
				W(7)=X1
				W(8)=Y1+YY
			ELSE			! VERTICAL BARS
				X1=(I-0.5)*VSP
				IF (KK.NE.0) THEN
				    K=MOD(I-1,KK)
				    X1=X1+(VSP-WB)*
     $					(FLOAT(K)-FLOAT(KK-1)/2.)
				ENDIF
				XX=WB
				IF (J.GT.1) THEN
					AV=0.0
					DO 113 K=1,J-1
						AV=AV+VALS(I,K)
113					CONTINUE
					Y1=(AV-VM)*SCALE
					AV=AV+VALS(I,J)
					YY=(AV-VM)*SCALE
				ELSE
					Y1=0.0
					YY=(VALS(I,J)-VM)*SCALE
				ENDIF
				W(1)=X1-XX
				W(2)=Y1
				W(3)=X1+XX
				W(4)=Y1
				W(5)=X1+XX
				W(6)=YY
				W(7)=X1-XX
				W(8)=YY
			ENDIF
C
C	OUTLINE AREA
C
			AN=0.0
			IL=0
			IF (DIS.LT.0.0) CALL NEWPEN(10*IWT(3))
			CALL SHADE(W(1),W(2),4,2,1,ABS(DIS),AN,
     $				W(9),IL,0.,1.,0.,1.)
			IF (ISH(J).EQ.0) GOTO 75
C
C	FILL WITH DESIRED PATTEN
C
			AN=45.0*(MOD(ISH(J)-1,4)-1)
			IL=10
			IF (ISH(J).GT.4.AND.ISH(J).LT.11) IL=11
			CALL SHADE(W(1),W(2),4,2,2,ABS(DIS),AN,
     $				W(9),IL,0.,1.,0.,1.)
			IF (ISH(J).GT.8) THEN
			    AN=AN+90.
			    IL=11
			    IF (ISH(J).GT.10) IL=10
			    CALL SHADE(W(1),W(2),4,2,2,ABS(DIS),AN,
     $				W(9),IL,0.,1.,0.,1.)
			ENDIF
75		CONTINUE
100	    CONTINUE
	    IF (IL.NE.10) CALL NEWPEN(1)
	ELSE
C
C	USE SHADING BETWEEN LINES
C
	   VSP=ABS(XLEN)/FLOAT(NB-1)
	   DO 300 J=1,NS
		DO 250 I=1,NB
			X1=(I-1)*VSP
			IF (J.GT.1) THEN
				AV=0.0
				DO 212 K=1,J
					AV=AV+VALS(I,K)
212				CONTINUE
				Y1=(AV-VM)*SCALE
			ELSE
				Y1=(VALS(I,J)-VM)*SCALE
			ENDIF
			W(2*I-1)=X1
			W(2*I)=Y1
250		CONTINUE
		IF (J.EQ.1) THEN
			I=NB+1
			W(2*I-1)=X1
			W(2*I)=0.
			I=I+1
			W(2*I-1)=0.
			W(2*I)=0.
		ELSE
			I=NB
			DO 260 II=1,NB
				I1=NB-II+1
				X1=(I1-1)*VSP
				AV=0.0
				DO 213 K=1,J-1
					AV=AV+VALS(I1,K)
213				CONTINUE
				Y1=(AV-VM)*SCALE
				I=I+1
				W(2*I-1)=X1
				W(2*I)=Y1
260			CONTINUE
			I=2*NB
		ENDIF
		NW=2*I+2
		IF(JF.EQ.2)CALL PLOT(FLOAT(ICOL(J+6)),0.,0)
C
C	PLOT LINE FOR THIS SEGMENT
C
		IF (DIS.LT.0.0) CALL NEWPEN(10*IWT(3))
		IP=3
		DO 275 K=1,I
			CALL PLOT(W(2*K-1),W(2*K),IP)
			IP=2
275		CONTINUE
		IF (ISH(J).EQ.0) GOTO 300
C
C	FILL WITH DESIRED PATTEN
C
		AN=45.0*(MOD(ISH(J)-1,4)-1)
		IL=10
		IF (ISH(J).GT.4.AND.ISH(J).LT.11) IL=11
		CALL SHADE(W(1),W(2),I,2,2,ABS(DIS),AN,
     $			W(NW),IL,0.,1.,0.,1.)
		IF (ISH(J).GT.8) THEN
			AN=AN+90.
			IL=11
			IF (ISH(J).GT.10) IL=10
			CALL SHADE(W(1),W(2),I,2,2,ABS(DIS),AN,
     $				W(NW),IL,0.,1.,0.,1.)
		ENDIF
300	   CONTINUE
	ENDIF
	IF (IL.NE.0) CALL NEWPEN(1)
C
C	ADD BASE LABELS
C
	IF (NBLAB.NE.0) THEN
		IF (DIS.LT.0.0) CALL NEWPEN(10*IWT(4))
		IF (JF.EQ.2) CALL PLOT(FLOAT(ICOL(6)),0.,0)
		K=NB
		IF (KK.NE.0) K=MOD(NB-1,KK)
		J=1
		IF (KK.NE.0) J=KK
		DO 320 I=1,K
		  IF (JFF.EQ.1) THEN	! HORIZONTAL BARS
		   Y1=((I-1)*J+0.5)*VSP
		   X1=-CS
		   CALL SYMBOL(X1,Y1,CS,BLAB(I),90.,NBLAB,0)
		  ELSE			! VERTICAL ORIENTATION
		   X1=((I-1)*J+0.5)*VSP
		   IF (JFF.EQ.2) X1=(I-1)*J*VSP
		   Y1=-CS
		   CALL SYMBOL(X1,Y1,CS,BLAB(I),0.,NBLAB,0)
		  ENDIF
320		CONTINUE
	ENDIF
C
C	PLOT LEGEND IF SELECTED
C
	IF (ALEG(1).EQ.0.0) GOTO 160
	IF (ALEG(1).LT.0.0) THEN
		X1=ABS(XLEN)+0.2
		Y1=(YLEN-NB*ABS(ALEG(1)))*0.5
	ELSE
		X1=ALEG(2)
		Y1=ALEG(3)
	ENDIF
	DO 225 I=1,NS
C
C	MAKE SHADING BOX
C
		W(1)=X1
		W(2)=Y1
		W(3)=X1+ABS(ALEG(1))
		W(4)=Y1
		W(5)=X1+ABS(ALEG(1))
		W(6)=Y1+ABS(ALEG(1))
		W(7)=X1
		W(8)=Y1+ABS(ALEG(1))
		XX=X1+ABS(ALEG(1))+0.12
		YY=Y1+(ABS(ALEG(1))-CS)*0.5
		AN=0.0
		IF(JF.EQ.2)CALL PLOT(FLOAT(ICOL(I+6)),0.,0)
		IL=10
		CALL SHADE(W(1),W(2),4,2,1,ABS(DIS),AN,
     $			W(9),IL,0.,1.,0.,1.)
		IF (ISH(I).EQ.0) GOTO 220
		AN=45.0*(MOD(ISH(I)-1,4)-1)
		IL=0
		IF (ISH(I).GT.4.AND.ISH(I).LT.11) IL=1
		CALL SHADE(W(1),W(2),4,2,2,ABS(DIS),AN,
     $			W(9),IL,0.,1.,0.,1.)
		IF (ISH(I).GT.8) THEN
		    AN=AN+90.
		    IL=1
		    IF (ISH(I).GT.10) IL=0
		    CALL SHADE(W(1),W(2),4,2,2,ABS(DIS),AN,
     $			W(9),IL,0.,1.,0.,1.)
		ENDIF
220		CONTINUE
C
C	ADD SEGMENT LEGEND LABELS
C
		IF (NSLAB.NE.0) THEN
		   IF (DIS.LT.0.0) CALL NEWPEN(10*IWT(4))
		   IF (JF.EQ.2) CALL PLOT(FLOAT(ICOL(5)),0.,0)
		   CALL SYMBOL(XX,YY,CS,SLAB(I),0.,NSLAB,-1)
		ENDIF
		Y1=Y1+ABS(ALEG(1))*1.2
225	CONTINUE
C
C	ADD TITLES
C
160	CONTINUE
	IF (NBT.GT.0) THEN
		IF (JF.EQ.2) CALL PLOT(FLOAT(ICOL(4)),0.,0)
	        IF (DIS.LT.0.0) CALL NEWPEN(IWT(4)*10)
		X1=0.
		Y1=0.
		CALL SYMBOL(X1,Y1,CS,BT,0.,NBT,-3)
		IF (JFF.NE.1) THEN		! VERTICAL
			X1=0.5*(ABS(XLEN)-X1)
			Y1=-3.0*CS
			CALL SYMBOL(X1,Y1,CS,BT,0.,NBT,-1)
		ELSE				! HORIZONTAL BARS
			Y1=0.5*(YLEN-X1)
			X1=-CS*3.0
			CALL SYMBOL(X1,Y1,CS,BT,90.,NBT,-1)
		ENDIF
	ENDIF
	IF (NLT.GT.0) THEN
		IF (JF.EQ.2) CALL PLOT(FLOAT(ICOL(4)),0.,0)
	        IF (DIS.LT.0.0) CALL NEWPEN(IWT(4)*10)
		X1=0.
		Y1=0.
		CALL SYMBOL(X1,Y1,CS,LT,0.,NLT,-3)
		IF (JFF.NE.1) THEN		! VERTICAL
			Y1=0.5*(YLEN-X1)
			X1=-AM-CS
			CALL SYMBOL(X1,Y1,CS,LT,90.,NLT,-1)
		ELSE				! HORIZONTAL BARS
			X1=0.5*(ABS(XLEN)-X1)
			Y1=-3.0*CS
			CALL SYMBOL(X1,Y1,CS,LT,0.,NLT,-1)
		ENDIF
	ENDIF
	IF (JF.EQ.2) CALL PLOT(FLOAT(ICOL(1)),0.,0)
	IF (NT.GT.0) THEN
		AN=-AN
	        IF (DIS.LT.0.0) CALL NEWPEN(IWT(6)*10)
		CALL SYMBOL(AN,YLEN+0.1,ABS(TCS),T,0.,NT,-1)
	ENDIF
C
C	FINISH UP
C
200	CONTINUE
	IF (DIS.LT.0.0) CALL NEWPEN(11)		!RESTORE LINETYPE/WIDTH
	CALL PLOT(0.,0.,3)			!PEN UP
	IF (IFLAG.GT.0) THEN
		CALL CTERM(2)			!CLEAR TERMINAL
		CALL PLOTND			!CLOSE PLOT OUTPUT
		REPEAT=.FALSE.
	ENDIF
	IF (IFLAG.LT.0) THEN
		REPEAT=.TRUE.
		CALL CTERM(1)			!PUT TERMINAL IN TEXT MODE
	ENDIF
	RETURN
	END
