C
C *** LAST REVISED ON 25-MAY-1995 09:48:16.41
C *** SOURCE FILE: [LONGLIB93.SOURCES.FORTRAN.MASTER]MVAX5D.FOR
C
	SUBROUTINE MVAX5D(V,ND1,ND2,ND3,ND4,N,A,B,IFLAG,IAX,IFORM,WORK,XH,YH,ZH,
     $		SPH,SPL,FAC,IW,IZ,ST,EN,T,NT,
     $		VT,NVT,VM,VX,ICOL)
C
C THIS ROUTINE PLOTS A FOUR DIMENSIONAL FUNCTION AS A 2-D ARRAY OF 3-D SURFACES
C USING THE MVAX3D SUBROUTINE.  COORDINATE SYSTEM USED IS SHOWN BELOW.  THE
C ROUTINE MAY PRODUCE SEVERAL PAGES OF OUTPUT WHICH CAN BE PASTED TOGETHER
C TO SEE THE ENTIRE ARRAY.  A FORMAT VALUE CAN BE USED TO INTERCHANGE THE
C DEPENDENT AXES OF THE PLOT TO PERMIT VIEWING THE FUNCTION IN DIFFERENT FORMS.
C A THREE DIMENSIONAL FUNCTION CAN BE PLOTTED BY USING A VALUE OF 1 FOR THE
C 4TH DIMENSION.
C
C COORDINATE SYSTEM USED:	IFORM=1 CORRESPONDS TO:
C
C       ^ W			  V(X,Y,Z,W)
C	|
C	|   V 			EVERY POINT IN X,Y IS PLOTTED.
C	|   |_X			ONLY THE IZth and IWth POINT (AS A NEW SURFACE)
C	| Y/			Z,W IS PLOTTED. 
C	--------> Z
C
C	V	(R) ARRAY TO BE PLOTTED DIMENSIONED V(ND1,ND2,ND3,ND4)
C	ND1..ND4(I) ARRAY OF DIMENSIONS OF V
C	N	(I) ARRAY OF NUMBER OF POINTS IN EACH DIMENSION OF V TO PLOT
C	A,B	(R) ALPHA, BETA ANGLES FOR MVAX3D
C	IFLAG	(I) OPTION FLAG
C			< 0 REPEAT PLOTTING, PLOT PACKAGE NOT CLOSED
C			= 0 PLOT PACKAGE CLOSED, NO PLOT PRODUCED
C			> 0 PLOT PACKAGE CLOSED, SINGLE PLOT PRODUCED
C	 (1000'S DIGIT) = 1 PLOT PACKAGE NOT INITIALIZED
C	 (ONE'S DIGIT)  = 2 COLOR INDEX ARRAY USED
C			= 1 COLOR TABLE IGNORED
C	 (TEN'S DIGIT)  = 0 PLOT MESH WITH SIDES, LOWER SURFACE NOT SHOWN
C			= 1 MESH W/O SIDES, LOWER SURFACE DOTTED
C			= 2 MESH W/O SIDES, LOWER SURFACE SOLID
C			= 3 HISTOGRAM COLUMS, LOWER SURFACE DOTTED
C			= 4 HISTOGRAM COLUMS, LOWER SURFACE SOLID
C	 (HUNDRED'S)	= 0 PROMPT FOR SCREEN GRAPHICS TYPE
C			<>0 SCREEN DEVICE NUMBER (SEE FRAME)
C	IAX	(I) AXIS OPTION FLAG
C			< 0 PLOT AXIS USING INPUT SCALE FACTORS VM,VX
C			= 0 AXIS NOT PLOTTED
C			> 0 PLOT AXIS USING MAX, MIN OF V ARRAY
C	 (ONE'S DIGIT)	= 1 USE ACTUAL MAX, MIN VALUES
C			= 2 USED SMOOTHED MAX, MIN VALUES
C	 (TEN'S DIGIT)  = 0 AXES PLOTTED FOR ALL SURFACES
C			= 1 AXES PLOTTED ONLY FOR FIRST SURFACE
C			= 2 AXES LABELED ONLY FOR FIRST SURFACE (PLOTTED FOR ALL)
C	IFORM	(I) PLOT FORMAT CODE
C			    AXIS USED FOR:	
C		     CODE     X  Y  Z  W
C			 1 :  1  2  3  4
C			 2 :  1  2  4  3
C			 3 :  1  4  2  3
C			 4 :  1  4  3  2
C			 5 :  1  3  2  4
C			 6 :  1  3  4  2
C			 7 :  2  1  3  4
C			 8 :  2  1  4  3
C			 9 :  2  4  3  1
C			10 :  2  4  1  3
C			11 :  2  3  4  1
C			12 :  2  3  1  4
C			13 :  3  1  2  4
C			14 :  3  1  4  2
C			15 :  3  2  1  4
C			16 :  3  2  4  1
C			17 :  3  4  1  2
C			18 :  3  4  2  1
C			19 :  4  1  2  3
C			20 :  4  1  3  2
C			21 :  4  2  1  3
C			22 :  4  2  3  1
C			23 :  4  3  1  2
C			24 :  4  3  2  1
C	WORK	(R) WORKING ARRAY DIMENSIONED N(X)*N(Y)
C	XH,YH,ZH (R) PLOTTING DIMENSIONS FOR SURFACE IN MVAX3D
C	SPH,SPL	(R) SIZE OF PLOT PAGE (HEIGHT,LENGTH)
C	FAC	(R) SHRINK FACTOR FOR SURFACES (2=FACTOR(1/2))
C	IW,IZ	(R) PLOT EVERY IWth and IZth SURFACE
C	ST	(R) ARRAY OF STARTING VALUES FOR AXES FOR EACH DIMENSION OF V
C	EN	(R) ARRAY OF ENDING VALUES FOR AXES FOR EACH DIMENSION OF V
C	T	(C) ARRAY OF TITLES FOR EACH DIMENSION OF V
C	NT	(I) ARRAY OF INTEGER LENGTHS OF TITLES
C OPTIONAL PARAMETERS ACCESSED ONLY IF IAX < 0 OR IFLAG ONE'S DIGIT=2
C	VM,VX	(R) MINIMUM AND MAXIMUM OF PLOT
C	ICOL	(I) ARRAY OF COLOR TABLE INDEXES
C
	DIMENSION V(ND1,ND2,ND3,ND4)
	DIMENSION N(4),WORK(1),ST(4),EN(4),NT(4),ICOL(1)
	CHARACTER*(*) T(4),VT
C
	DIMENSION ND(4),I(4)
	INTEGER X(24),Y(24),Z(24),W(24)
	DATA X/1,1,1,1,1,1,2,2,2,2,2,2,3,3,3,3,3,3,4,4,4,4,4,4/
	DATA Y/2,2,4,4,3,3,1,1,4,4,3,3,1,1,2,2,4,4,1,1,2,2,3,3/
	DATA Z/3,4,2,3,2,4,3,4,3,1,4,1,2,4,1,4,1,2,2,3,1,3,1,2/
	DATA W/4,3,3,2,4,2,4,3,1,3,1,4,4,2,4,1,2,1,3,2,3,1,2,1/
C
	DIMENSION AS(2)
	LOGICAL REPEAT
	SAVE REPEAT
	DATA REPEAT/.FALSE./,DTR/0.0174532925/
C
	IF (IFLAG.EQ.0) THEN
		IF (REPEAT) CALL PLOTND	!CLOSE PLOTTER
		REPEAT=.FALSE.
		RETURN
	ENDIF
C
	IF (IAX.LT.0) THEN	! USE INPUT MAX,MIN
		AMAX=VX
		AMIN=VM
	ELSE			! DETERMINE MAX,MIN ARRAY VALUES
		AMAX=V(1,1,1,1)
		AMIN=AMAX
		DO 16 I1=1,N(1)
			DO 16 I2=1,N(2)
				DO 16 I3=1,N(3)
					DO 16 I4=1,N(4)
				AMAX=AMAX1(AMAX,V(I1,I2,I3,I4))
				AMIN=AMIN1(AMIN,V(I1,I2,I3,I4))
16		CONTINUE
	ENDIF
	IF (MOD(IABS(IAXIS),10).EQ.2) THEN	! SMOOTH SCALE FACTORS
		AS(1)=AMAX
		AS(2)=AMIN
		CALL SCALE(AS,YH,2,1,1,AMIN,DAA)
		AMAX=YLEN*DAA+AMIN
	ENDIF
C
C	INTIALIZE PLOT PACKAGE
C
	JF=IABS(IFLAG)
	IF (.NOT.REPEAT.AND.JF.LT.10000) THEN
		ILU=JF/100
		CALL FRAME(3,ILU,.8,.4,1.)		!INTIALIZE
	ELSE
		CALL CTERM(-1)				!PUT TERMINAL IN PLOT MODE
	ENDIF
C
	IF (IFORM.LT.1.OR.IFORM.GT.24) GOTO 1000
	JFF=-(10000+MOD(JF,100))
	IA=-1
	SP=0.3						!SPACING BETWEEN SURFACES
	IF (IAX.NE.0.AND.IAX/10.EQ.0) SP=1.2
C
C	REARRANGE AXISES
C
	NNX=NT(X(IFORM))
	NNZ=NT(Y(IFORM))
	NNW=NT(W(IFORM))
	NNT=NT(Z(IFORM))
	NNV=NVT
	IF (IAX.EQ.0) THEN
		NNX=0
		NNZ=0
		NNV=0
	ENDIF
C
	XS=ST(X(IFORM))
	XE=EN(X(IFORM))
	YS=ST(Y(IFORM))
	YE=EN(Y(IFORM))
C
C	DETERMINE THE NUMBER OF PAGES
C
	NX=N(X(IFORM))
	NY=N(Y(IFORM))
	NZ=(N(Z(IFORM))-1)/IZ+1
	NW=(N(W(IFORM))-1)/IW+1
	NZ1=NZ
	IF (NZ1.GT.1) NZ1=NZ-1
	NW1=NW
	IF (NW1.GT.1) NW1=NW-1
C
	AL=XH*COS(A*DTR)+COS(B*DTR)*ZH+SP		! SURFACE LENGTH
	AH=XH*SIN(A*DTR)+ZH*SIN(B*DTR)+YH+SP-0.5	! SURFACE HEIGHT
	XX=XH*COS(A*DTR)+0.5
	NPZ=IFIX((AL*NZ)/SPL/FAC)+1
	NPW=IFIX((AH*NW)/SPH/FAC)+1
C
C	SET SCALE FACTOR DOWN
C
	CALL FACTOR(1./FAC)
C
C	MAKE A PASS FOR EACH PAGE
C
	DO 500 IPW=1,NPW
	  IF (IPW.GT.1) THEN
		CALL PLOT(0.,0.,3)
		CALL NEWPAGE
		CALL CTERM(2)
		CALL RTERM(2)
		CALL CTERM(-1)
	  ENDIF
	  XWP=SPH*(IPW-1)*FAC
	DO 500 IPZ=1,NPZ
	  IF (IPZ.GT.1) THEN
		CALL PLOT(0.,0.,3)
		CALL NEWPAGE
		CALL CTERM(2)
		CALL RTERM(2)
		CALL CTERM(-1)
	  ENDIF
	  YZP=SPL*(IPZ-1)*FAC
	IF (IPZ.EQ.1.AND.IPW.EQ.1) THEN
	  CALL SYMBOL(-.49,0.0,.2,T(W(IFORM)),90.,NNW,-1)
	  CALL SYMBOL(0.1,-.5,.2,T(Z(IFORM)),0.,NNT,-1)
	  CALL SYMBOL(999.,999.,.2,'  Page (',0.,8,-1)
	  CALL NUMBER(999.,999.,.2,FLOAT(IPZ),0.,0.0,-1)
	  CALL SYMBOL(999.,999.,.2,',',0.,1,-1)
	  CALL NUMBER(999.,999.,.2,FLOAT(IPX),0.,0.0,-1)
	  CALL SYMBOL(999.,999.,.2,') of (',0.,6,-1)
	  CALL NUMBER(999.,999.,.2,FLOAT(NPZ),0.,0.0,-1)
	  CALL SYMBOL(999.,999.,.2,',',0.,1,-1)
	  CALL NUMBER(999.,999.,.2,FLOAT(NPX),0.,0.0,-1)
	  CALL SYMBOL(999.,999.,.2,')',0.,1,-1)
	ENDIF
C
C	FOR EACH W AND Z POINT, CREATE A 3-D PLOT
C
	DO 300 KW=1,NW
	  JW=1+(KW-1)*IW
	  I(W(IFORM))=JW
	DO 300 KZ=1,NZ
	  JZ=1+(KZ-1)*IZ
	  I(Z(IFORM))=JZ
C
	YZ=AL*(KZ-1)-YZP
	XW=AH*(KW-1)-XWP
C
C	CHECK TO SEE IF WE DON'T NEED TO PLOT THIS SURFACE
C
	IF (YZ.GT.FAC*SPL.OR.YZ.LT.-AL) GOTO 300
	IF (XW.GT.FAC*SPH.OR.XW.LT.-AH) GOTO 300
C
C	MOVE PLOT ORIGIN
C
	CALL PLOT(YZ,XW,-3)
C
C	LABEL PLOT
C
	CS=0.18
	CALL CTERM(-1)
	CALL SYMBOL(XX-0.5,-0.25,CS,'(',0.,1,-1)
	CALL NUMBER(999.,999.,CS,FLOAT(JZ),0.,0.0,-1)
	CALL SYMBOL(999.,999.,CS,',',0.,1,-1)
	CALL NUMBER(999.,999.,CS,FLOAT(JW),0.,0.0,-1)
	IF (IAX.NE.0) THEN
		WVAL=(JW-1)*(EN(W(IFORM))-ST(W(IFORM)))/NW1+ST(W(IFORM))
		ZVAL=(JZ-1)*(EN(Z(IFORM))-ST(Z(IFORM)))/NZ1+ST(Z(IFORM))
		CALL SYMBOL(999.,999.,CS,') (',0.,3,-1)
		CALL NUMBER(999.,999.,CS,ZVAL,0.,0.02,-1)
		CALL SYMBOL(999.,999.,CS,',',0.,1,-1)
		CALL NUMBER(999.,999.,CS,WVAL,0.,0.02,-1)
	ENDIF
	CALL SYMBOL(999.,999.,CS,')',0.,1,-1)
C
C	FILL WORKING ARRAY
C
	DO 150 IY=1,NY
		I(Y(IFORM))=IY
		DO 150 IX=1,NX
			I(X(IFORM))=IX
			WORK(IY+NY*(IX-1))=V(I(1),I(2),I(3),I(4))
150	CONTINUE
C
C	CALL MVAX3D TO DO THE WORK
C
	IF (MOD(JFF,10).EQ.2) THEN		! PLOT WITH COLOR
	   CALL MVAX3D(WORK,NX,NY,NX,NY,A,B,XH,YH,ZH,JFF,IA,T(X(IFORM)),
     $		NNX,XS,XE,VT,NNV,T(Y(IFORM)),NNZ,YS,YE,AMIN,AMAX,ICOL)
	ELSE					! PLOT W/O COLOR
	   CALL MVAX3D(WORK,NX,NY,NX,NY,A,B,XH,YH,ZH,JFF,IA,T(X(IFORM)),
     $		NNX,XS,XE,VT,NNV,T(Y(IFORM)),NNZ,YS,YE,AMIN,AMAX)
	ENDIF
C
C	TURN OFF AXES AFTER FIRST PLOT IF DESIRED
C
	IF (IAX/10.EQ.2) THEN
		NNX=MOD(NNX,100)+100
		NNZ=MOD(NNZ,100)+100
		NNV=MOD(NNV,100)+100
	ELSE IF (IAX/10.NE.0) THEN
		NNX=0
		NNZ=0
		NNV=0
	ENDIF
C
C	RESTORE PLOT ORIGIN
C
	CALL PLOT(-YZ,-XW,-3)
300	CONTINUE
500	CONTINUE
C
C	RESTORE SCALE FACTOR
C
	CALL FACTOR(FAC)
C
C	FINISH UP PLOT
C
1000	CALL PLOT(0.,0.,3)			!PEN UP
	IF (IFLAG.GT.0) THEN
		CALL CTERM(2)			!CLEAR TERMINAL
		CALL PLOTND			!CLOSE PLOT OUTPUT
		REPEAT=.FALSE.
	ENDIF
	IF (IFLAG.LT.0) THEN
		REPEAT=.TRUE.
		CALL CTERM(1)			!PUT TERMINAL IN TEXT MODE
	ENDIF
	RETURN
	END
