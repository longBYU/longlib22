C
C *** LAST REVISED ON  8-DEC-1993 07:03:25.89
C *** SOURCE FILE: [LONGLIB93.SOURCES.FORTRAN.MASTER]MLIB.FOR
C
	SUBROUTINE PLOTSC(X,Y,NP,IFLAG,XAXL,YAXL,XTITL,NXT,
     1		YTITL,NYT,TITLE,NT,XMIN,XMAX,YMIN,YMAX,ICOL,ILINE)
C
C	CREATED BY D. LONG    OCT, 1983	AT JPL
C	MODIFIED BY D. LONG   FEB, 1986 AT JPL + ADDED LINE TYPE
C
C	ROUTINE TO PLOT A LINE OF DATA
C
C	X	REAL X VALUE ARRAY
C	Y	REAL Y DATA ARRAY
C	NP	INT  # OF POINTS IN ARRAY
C	IFLAG	INT
C	 (MAG)	>10000 REPEAT PLOTTING BUT DO NOT INTIALIZE PLOTTER
C		< 0  REPEAT PLOTTING (DON'T CLOSE PLOTTER OUTPUT)
C		= 0  CLOSE PLOTTER OUTPUT--DO NOT PRODUCE PLOT
C		> 0  SINGLE PLOT OUTPUT ONLY (PLOTTER OUTPUT CLOSED)
C (ONE'S DIGIT) =0  DO NOT PLOT AXISES OR TITLE
C 		=1  PLOT BOX WITH AXIS TICK MARKS
C		=2  PLOT A SOLID GRID
C		=3  PLOT TICKED GRID W/O BOX
C		=4  PLOT TICKED GRID WITH BOX
C		=5  PLOT BOX WITH AXIS TICK MARKS AND TICKED GRID
C		=6  PLOT WITH NO BOX OR GRID
C (TEN'S DIGIT) =0  ASK WHICH SCREEN DEVICE TO USE
C		<>0 SCREEN DEVICE CODE
C	XAXL	REAL  X AXIS LENGTH IN INCHES
C		< 0  USE INPUT VALUE SCALING--AXIS PLOTTED
C		> 0  USE AUTO SCALING--AXIS PLOTTED
C	YAXL	REAL  Y AXIS LENGTH IN INCHES
C		< 0  USE INPUT VALUE SCALING--AXIS PLOTTED
C		> 0  USE AUTO SCALING--AXIS PLOTTED
C NOTE: 	XMIN,XMAX,YMIN,YMAX ARE ONLY ACCESSED IF XAXL OR YAXL < 0
C
C	XTITL	CHAR  X AXIS TITLE
C	NXT	INT   NUMBER OF CHARACTERS IN X AXIS TITLE STRING
C		< 0  TICKS ON INSIDE OF AXIS
C		= 0  NO AXIS
C		> 0  TICKS ON TITLE SIDE OF AXIS
C	YTITL	CHAR  Y AXIS TITLE
C	NYT	INT   NUMBER OF CHARACTERS IN Y AXIS TITLE STRING
C		< 0  TICKS ON INSIDE OF AXIS
C		= 0  NO AXIS
C		> 0  TICKS ON TITLE SIDE OF AXIS
C	TITLE	CHAR  PLOT TITLE
C	NT	INT   NUMBER OF CHARACTERS IN PLOT TITLE (<100)
C		= 0  NO TITLE
C		< 0  USE PEN COLOR ARRAY (XMIN-YMAX ALSO ACCESSED)
C			IF ABS(NT)/100 > 0 USE LINE TYPE
C	XMIN	REAL  MINIMUM VALUE DISPLAYED ON X AXIS
C	XMAX	REAL  MAXIMUM VALUE DISPLAYED ON X AXIS
C	YMIN	REAL  MINIMUM VALUE OF Y ARRAY
C	YMAX	REAL  MAXIMUM VALUE OF Y ARRAY
C	ICOL	INT   PEN COLOR CONTROL ARRAY (ACCESSED ONLY IF NT < 0)
C			ICOL(1) GRID COLOR
C			ICOL(2) AXIS LINE COLOR
C			ICOL(3) AXIS NUMBERS COLOR
C			ICOL(4) AXIS LABEL COLOR
C			ICOL(5) AXIS EXPONENT COLOR
C			ICOL(6) PLOTTED LINE COLOR
C			ICOL(7) TITLE COLOR (RETURN COLOR)
C	ILINE	INT  LINE TYPE OF PLOTTED LINE
C
	CHARACTER*(*) XTITL,YTITL,TITLE
	REAL Y(NP),X(NP)
	LOGICAL REPEAT
	INTEGER PL,ICOL(*),IC(4)
	COMMON /CPLOTSC/XM,DX,YM,DY
	SAVE /CPLOTSC/,REPEAT
	DATA REPEAT/.FALSE./
C
	PL=3
	IF (IFLAG.EQ.0) THEN
		IF (REPEAT) CALL PLOTND !CLOSE PLOT PACKAGE
		REPEAT=.FALSE.
		RETURN
	ENDIF
	JF=IABS(IFLAG)
	IF (.NOT.REPEAT.AND.JF.LT.10000) THEN
		ILU=-(JF/10)
		CALL FRAME(PL,ILU,1.2,1.2,1.)	!INTIALIZE PLOT PACKAGE
	ELSE
		CALL CTERM(-1)			!RESTORE PLOT MODE
C		CALL NEWPAGE			!NEW PAGE
		JF=MOD(JF,10)
	ENDIF
	JF=MOD(JF,10)
	NX=int(ABS(ANINT(XAXL)))		!GRID SIZES
	NY=int(ABS(ANINT(YAXL)))
	CALL SCALE(X,ABS(XAXL),NP,1,1,XM,DX)	!SMOOTH SCALE FACTORS
	IF (XAXL.LT.0.0) THEN			!INPUT SCALE FACTORS
		XM=XMIN
		DX=(XMAX-XMIN)/ABS(XAXL)
	ENDIF
	CALL SCALE(Y,ABS(YAXL),NP,1,1,YM,DY)	!SMOOTH SCALE FACTORS
	IF (YAXL.LT.0.0) THEN			!INPUT SCALE FACTORS
		YM=YMIN
		DY=(YMAX-YMIN)/ABS(YAXL)
	ENDIF
	IF (JF.EQ.0) GOTO 77
	IF (JF.EQ.6) GOTO 76
	IF (JF.GT.1) THEN			!GRID
		IF (JF.GE.3) NX=-NX		!TICKED GRID
		IF (JF.GT.4) NY=-NY		!BOXED TICKED GRID
		IF (NT.LT.0) CALL PLOT(FLOAT(ICOL(1)),0.,0)! PEN COLOR
		CALL GRID(0.,0.,1.,1.,NX,NY)	!PLOT GRID
		IF (JF.EQ.5) JF=1
	ENDIF
76	IX=IABS(NXT)
	XL=ABS(XAXL)*SIGN(1.,FLOAT(NXT))
	NADD=0
	IF (NT.LT.0) THEN
		IC(1)=ICOL(2)
		IC(2)=ICOL(3)
		IC(3)=ICOL(4)
		IC(4)=ICOL(5)
		NADD=100000		! PEN COLOR
	ENDIF
	IF (NXT.NE.0) THEN
		CALL AXIS(0.,0.,XTITL,-IX-NADD,XL,0.,XM,DX,
     $			ND1,ND2,IC) !X-AXIS
	IF (JF.EQ.1) CALL AXIS(0.,ABS(YAXL),XTITL,IX+100+NADD,XL,0.,XM,DX,
     $			ND1,ND2,IC) !X-AXIS
	ENDIF
	IY=IABS(NYT)+1000
	YL=ABS(YAXL)*SIGN(1.,FLOAT(NYT))
	IF (NYT.NE.0) THEN
		CALL AXIS(0.,0.,YTITL,IY+NADD,YL,90.,YM,DY,ND1,ND2,IC) !Y-AXIS
		IF (JF.EQ.1) CALL AXIS(ABS(XAXL),0.,YTITL,-IY-100-NADD,
     $			YL,90.,YM,DY,ND1,ND2,I) 	!Y-AXIS
	ENDIF
77	IF (NT.LT.0) CALL PLOT(FLOAT(ICOL(6)),0.,0)	!PEN COLOR
	IF (IABS(NT)/100.GT.0) CALL NEWPEN(ILINE)	!LINE TYPE
	CALL LINE(X,Y,NP,1,0,0,1,1,XM,DX,YM,DY) 	!PLOT DATA
	CS=.21						!TITLE CHARACTER SIZE
	IF (NT.LT.0) CALL PLOT(FLOAT(ICOL(7)),0.,0)	!PEN COLOR
	IF (IABS(NT)/100.GT.0) CALL NEWPEN(1)		!LINE TYPE
	IF (NT.NE.0.AND.JF.NE.0) THEN			!PLOT TITLE
		XP=0.
		YP=0.
		CALL SYMBOL(XP,YP,CS,TITLE,
     $			0.,MOD(IABS(NT),100),-3)	!TITLE LENGTH
		XP=ABS(XAXL/2.)-XP/2.
		IF (XP.LT.0.) XP=0.
		CALL SYMBOL(XP,ABS(YAXL)+.15,CS,TITLE,
     $			0.,MOD(IABS(NT),100),-1)	!PLOT TITLE
	ENDIF
	CALL PLOT(0.,0.,3)				!PEN UP
	IF (IFLAG.GT.0) THEN
		CALL CTERM(2)				!ASK IF CLEAR SCREEN
		CALL PLOTND				!CLOSE PLOTTER
		REPEAT=.FALSE.
	ENDIF
	IF (IFLAG.LT.0) THEN
		REPEAT=.TRUE.
		CALL CTERM(1)				!TERMINAL TO TEXT MODE
	ENDIF
	RETURN
	END
