C
C *** LAST REVISED ON  8-DEC-1993 07:03:25.89
C *** SOURCE FILE: [LONGLIB93.SOURCES.FORTRAN.MASTER]MLIB.FOR
C
	SUBROUTINE PHIST(A,NPTS,TITLE,NT,XL,YL,SUB,NS,XLABEL,
     $		NX,XMIN,XMAX,AMAX,IFLAG,ISHAD,AMEAN,SIGMA,ICOL)
C
C	CREATED BY D. LONG    AUG, 1983	AT JPL
C
C	GENERALIZED ROUTINE TO PLOT HISTOGRAMS
C
C	A	REAL	ARRAY CONTAINING HEIGHT OF HISTOGRAM COLUMNS
C	NPTS	INT	NUMBER OF POINTS IN A
C	TITLE	CHAR	TITLE OF HISTOGRAM
C	NT	INT	NUMBER OF CHARACTERS IN TITLE STRING
C			< 0 USE COLOR ARRAY
C	XL	REAL	LENGTH OF X AXIS
C	YL	REAL	LENGTH OF Y AXIS
C	SUB	CHAR	SUBTITLE OF HISTOGRAM
C	NS	INT	NUMBER OF CHARACTERS IN SUB STRING
C	XLABEL	CHAR	X AXIS LABEL OF HISTOGRAM
C	NX	INT	NUMBER OF CHARACTERS IN XLABEL STRING
C	XMIN	REAL	MINIMUM VALUE TO BE PLOTTED ON X AXIS
C	XMAX	REAL	MAXIMUM VALUE TO BE PLOTTED ON X AXIS
C	AMAX	REAL	MAXIMUM VALUE IN ARRRAY
C	IFLAG	INT	FUNCTION CONTROL FLAG
C			>10000	REPEAT PLOTTING BUT DO NOT INTIALIZE PLOTTER
C			< 0	REPEAT PLOTTING (DON'T CLOSE PLOTTER OUTPUT)
C			= 0	CLOSE PLOTTER OUTPUT--DO NOT PRODUCE PLOT
C			> 0	SINGLE PLOT OUTPUT ONLY (PLOTTER OUTPUT CLOSED)
C		ONE'S DIGIT = 2 PLOT A VALUE ATOP HISTOGRAM COLUMN
C			    = 1 DO NOT PLOT NUMBERS
C			    = 0 DO NOT PLOT AXISES
C		TEN'S DIGIT     SCREEN DEVICE CONTROL
C			    = 0 ASK SCREEN DEVICE TYPE
C	ISHAD	INT	SHADING CONTROL FLAG
C			< 0	SHADE WITH SOLID LINE
C			= 0	NO SHADING
C			> 0	SHADE WITH LINE TYPE ISHAD
C	AMEAN	REAL	LINE INDICATING MEAN (IN UNITS OF XMIN, XMAX)
C	SIGMA	REAL	LINES INDICATING 1 SIGMA DEVIATIONS FROM MEAN
C			> 0	MEAN AND SIGMA VALUES PLOTTED
C			= 0	ONLY MEAN VALUE PLOTTED
C			< 0	MEAN AND SIGMA VALUES NOT PLOTTED
C	ICOL	INT	PEN COLOR LIST (ACCESSED ONLY IF NT < 0
C			ICOL(1) AXIS LINE COLOR
C			ICOL(2) AXIS NUMBERS COLOR
C			ICOL(3) AXIS TITLE COLOR
C			ICOL(4) AXIS EXPONENT COLOR
C			ICOL(5) TITLE COLOR
C			ICOL(6) SUBTITLE COLOR
C			ICOL(7) MEAN MARK COLOR
C			ICOL(8) SIGMA MARK COLOR
C			ICOL(9) HISTOGRAM COLUMN COLORS (RETURN COLOR)
C			ICOL(10) HISTOGRAM COLUM LABEL COLOR
C
	DIMENSION A(NPTS),W(15),XS(5),YS(5),ICOL(*),IC(4)
	CHARACTER*(*) TITLE,SUB,XLABEL
	LOGICAL REPEAT
	COMMON /CPHIST/XM,DX,YM,DY
	SAVE /CPHIST/,REPEAT
	DATA REPEAT/.FALSE./,XORG/1./,YORG/1.1/,YLEN/4./XLEN/7./
	DATA CS/.2/,CST/.25/,CSM/.14/				! TITLE SIZES
	DATA T/.2/,T1/.1/					! TICK SIZES
C
	IF (IFLAG.EQ.0) THEN
		IF (REPEAT) CALL PLOTND 	! CLOSE PLOT PACKAGE
		REPEAT=.FALSE.
		RETURN
	ENDIF
	XLEN=ABS(XL)
	YLEN=ABS(YL)
	IF (XLEN.EQ.0..OR.YLEN.EQ.0.) RETURN	! ERROR RETURN
	JF=IABS(IFLAG)
	IF (.NOT.REPEAT.AND.JF.LT.10000) THEN
		ILU=-JF/10
		CALL FRAME(3,ILU,XORG,YORG,1.)		! OPEN PLOT OUTPUT
	ELSE
		CALL CTERM(-1)				! RETURN SCREEN TO GRAPHICS
C		CALL NEWPAGE				! START NEW PAGE
		JF=MOD(JF,10)
	ENDIF
	JF=MOD(JF,10)
	M=0
	IF (ISHAD.GT.0.AND.ISHAD.LT.11) M=ISHAD		! SELECT SHADE LINE TYPE
	IFLG=MOD(IABS(IFLAG),10)
	YM=0.						! BOTTOM Y AXIS
	DY=(AMAX-YM)/YLEN				! Y SCALE FACTOR
	NADD=0
	IF (NT.LT.0) THEN
		NADD=100000				! COLOR AXIS
		IC(1)=ICOL(1)
		IC(2)=ICOL(2)
		IC(3)=ICOL(3)
		IC(4)=ICOL(4)
	ENDIF
	IF (JF.NE.0) CALL AXIS(0.,0.,' ',1001+NADD,YLEN,
     $			90.,YM,DY,N1,N2,IC)	! PLOT Y AXIS
	NNX=NX
	IF (NNX.LT.1) NNX=1
	DX=(XMAX-XMIN)/XLEN
	XM=XMIN
	IF (JF.NE.0) CALL AXIS(0.,0.,XLABEL,-NNX-NADD,
     $			XLEN,0.,XM,DX,N1,N2,IC)	! PLOT X AXIS
	XP=0.
	YP=YLEN+CS+CSM+.25
	IF (JF.NE.0) THEN
	IF (NT.LT.0) CALL PLOT(FLOAT(ICOL(5)),0.,0)	! PEN COLOR
	IF (NT.NE.0) CALL SYMBOL(XP,YP,CST,TITLE,0.,IABS(NT),-1)! TITLE
	IF (NT.LT.0) CALL PLOT(FLOAT(ICOL(6)),0.,0)	! PEN COLOR
	IF (NS.GT.0) CALL SYMBOL(XP,YP-CS-.05,CS,SUB,0.,NS,-1)	! SUBTITLE
	IF (NT.LT.0) CALL PLOT(FLOAT(ICOL(7)),0.,0)	! PEN COLOR
	IF (SIGMA.GE.0.0) THEN				! PLOT MEAN
		X1=(AMEAN-XMIN)/DX			! LOCATE MEAN
		Y0=0.
		Y1=YLEN+CSM
		X2=X1-1.5*CSM
		CALL PLOT(X1-T1,Y0-T1,3)		! START TICK MARK
		CALL PLOT(X1,Y0,2)			! CENTER TICK MARK
		CALL PLOT(X1+T1,Y0-T1,2)		! END TICK MARK
		CALL PLOT(X1,Y0-T1,3)			! START LINE
		CALL PLOT(X1,Y1,2)			! END LINE
		CALL PLOT(X1-T,Y1,3)			! PUT ON HAT
		CALL PLOT(X1+T,Y1,2)			! END HAT
		CALL SYMBOL(X2,Y1+.05,CSM,'MEAN',0.,4,-1)
	IF (NT.LT.0) CALL PLOT(FLOAT(ICOL(8)),0.,0)	! PEN COLOR
		IF (SIGMA.GT.0.0) THEN			! PLOT SIGMA
			Y1=Y1-CSM-.05
			X2=(AMEAN-SIGMA-XMIN)/DX	! MEAN - SIGMA
			X3=(AMEAN+SIGMA-XMIN)/DX	! MEAN + SIGMA
			X4=2.1*CSM
			CALL PLOT(X2-T1,Y0-T1,3)	! START TICK MARK
			CALL PLOT(X2,Y0,2)		! CENTER TICK MARK
			CALL PLOT(X2+T1,Y0-T1,2)	! END TICK MARK
			CALL PLOT(X2,Y0-T1,3)		! START LINE
			CALL PLOT(X2,Y1,2)		! END LINE
			CALL PLOT(X2-T,Y1,3)		! PUT ON HAT
			CALL PLOT(X2+T,Y1,2)		! END HAT
			CALL SYMBOL(X2-X4,Y1+.05,CSM,'SIGMA',0.,5,-1)
			CALL PLOT(X3-T1,Y0-T1,3)	! START TICK MARK
			CALL PLOT(X3,Y0,2)		! CENTER TICK MARK
			CALL PLOT(X3+T1,Y0-T1,2)	! END TICK MARK
			CALL PLOT(X3,Y0-T1,3)		! START LINE
			CALL PLOT(X3,Y1,2)		! END LINE
			CALL PLOT(X3-T,Y1,3)		! PUT ON HAT
			CALL PLOT(X3+T,Y1,2)		! END HAT
			CALL SYMBOL(X3-X4,Y1+.05,CSM,'SIGMA',0.,5,-1)
		ENDIF
	ENDIF
	ENDIF
C
	WIDTH=XLEN/FLOAT(NPTS)
	CW=WIDTH-.02
	IF (CW.LT.0.) CW=WIDTH
	IF (WIDTH.GT.0.18) CW=0.18
	ANG=45.
	X0=0.
	Y0=YM
	X1=WIDTH/2.-CW/2.
C
	IF (NT.LT.0) CALL PLOT(FLOAT(ICOL(9)),0.,0)	! PEN COLOR
	DO 100 I=1,NPTS
		Y1=(A(I)-YM)/DY
		XS(1)=X0
		YS(1)=Y0+.02
		XS(2)=X0
		YS(2)=Y1
		CALL RECT(X0,Y0,X0+WIDTH,Y1)		! PLOT HISTOGRAM
		X0=X0+WIDTH
		XS(3)=X0
		YS(3)=Y1
		XS(4)=X0
		YS(4)=Y0+.02
		IF (IFLG.EQ.2) THEN
	IF (NT.LT.0) CALL PLOT(FLOAT(ICOL(10)),0.,0)	! PEN COLOR
			CALL NUMBER(X0-X1,Y1+.08,CW,
     $					A(I),90.,0.0,-1)
	IF (NT.LT.0) CALL PLOT(FLOAT(ICOL(9)),0.,0)	! PEN COLOR
		ENDIF
		IF (M.NE.0.AND.Y1.GT..02) THEN		! ADD SHADING
			CALL SHADE(XS,YS,4,1,2,.06,ANG,W,M,0.,1.,0.,1.)
			IF (ANG.EQ.45.) THEN		! ALTERNATE ANGLES
				ANG=135.
			ELSE
				ANG=45.
			ENDIF
		ENDIF
100	CONTINUE
	CALL PLOT(0.,0.,3)
	IF (IFLAG.GT.0) THEN
		CALL CTERM(2)				!ASK IF CLEAR SCREEN
		CALL PLOTND				!CLOSE PLOTTER
		REPEAT=.FALSE.
	ENDIF
	IF (IFLAG.LT.0) THEN
		REPEAT=.TRUE.
		CALL CTERM(1)				!PUT TERMINAL IN TEXT MODE
	ENDIF
	RETURN
	END
