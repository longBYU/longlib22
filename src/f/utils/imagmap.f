	PROGRAM IMAGMAP
C *** LAST REVISED ON 12-APR-1991 14:35:42.27
C *** SOURCE FILE: [LONGD.GRAPHICS]IMAGMAP.FOR
C
	DIMENSION ISTVAL(40000),STVAL(40000)
C
C	OPEN PLOT PACKAGE
C
	CALL FRAME(0,0,1.,1.,1.)
C
C	SET SIZE AND SCALE FACTORS
C
	NSX=100
	NSY=100
	XSIZE=4.0
	YSIZE=4.0
C
C	FILL ARRAY
C
	DO I=1,NSX
		DO J=1,NSY
			N=I+(J-1)*NSX
			STVAL(N)=1.+COS(I*6./FLOAT(NSX))*COS(J*5./FLOAT(NSY))
		END DO
	END DO
C
C	DISPLAY IMAGE MAP TO TEKTRONICS TERMINAL
C
	CALL TERSCL(1,IT,0.,0.,IXT,IYT)
	CALL TERSCL(1,IT,XSIZE,YSIZE,IXT1,IYT1)
	IF (IT.GT.0) THEN		! IF PROPER TEK TERMINAL
	  RJXT=FLOAT(IXT1-IXT)/FLOAT(NSX)
	  RJYT=FLOAT(IYT1-IYT)/FLOAT(NSY)
C
C	SCALE TO THE RANGE -15 TO 0
C
	  DO I=1,NSX*NSY
		ISTVAL(I)=-NINT(STVAL(I)*7.5)
	  END DO
	  CALL CTERM(-1)
	  CALL TEKMAP(ISTVAL,NSX,NSY,NSX,NSY,IXT,IYT,RJXT,RJYT)
	ENDIF
C
C	DISPLAY IMAGE MAP TO RAMTEK DEVICE
C
	CALL RMPIX(0.,0.,IXR,IYR)
	CALL RMPIX(XSIZE,YSIZE,IX1,IY1)
	RJX=(FLOAT(IABS(IX1-IXR))/FLOAT(NSX))
	RJY=(FLOAT(IY1-IYR)/FLOAT(NSY))
	XSC=XSIZE/NSX
	YSC=YSIZE/NSY
C
C	SCALE TO (0..255)
C
	DO I=1,NSX*NSY
		ISTVAL(I)=NINT(STVAL(I)*127)
	END DO
C
C	PLOT TO RAMTEK ONLY IF RAMTEK IS OPEN
C
	ICHAN=IRMCHAN(ID)
	IF (ICHAN.GT.0) THEN
	  CALL RAMMAP(ISTVAL,NSX,NSY,NSX,NSY,IXR,IYR,RJX,RJY)
	ENDIF
C
C	SEND IMAGE MAP TO META FILE
C
c	CALL METMAP(ISTVAL,NSX,NSY,NSX,NSY,0.,0.,XSIZE,YSIZE)
	CALL METMAP2(ISTVAL,NSX,NSY,1,1,NSX,NSY,0.,0.,XSIZE,YSIZE)
	CALL PLOT(4.,4.,-3)
	CALL METMAP2(ISTVAL,NSX,NSY,50,50,100,100,0.,0.,XSIZE,YSIZE)
C
C	NOW AXES PLOT TO ALL DEVICES
C
	CALL PLOT(1.,0.,0)		! SET PLOT COLOR
C
	CALL RECT(0.,0.,XSIZE,YSIZE)
	AF=ANINT(XSIZE)+1.0101
	CALL AXIS3(0.,0.,'X',-1,XSIZE,0.,0.0,XSIZE,AF,0.12,6.2)
	CALL AXIS3(0.,YSIZE,'X',101,XSIZE,0.,0.0,XSIZE,AF,0.12,6.2)
	AF=ANINT(YSIZE)+1.0101
	CALL AXIS3(0.,0.,'Y',1,YSIZE,90.,0.0,YSIZE,AF,0.12,6.2)
	CALL AXIS3(XSIZE,0.,'Y',-101,YSIZE,90.,0.0,YSIZE,AF,0.12,6.2)
	CALL PLOT(0.,0.,5)
C
C	CLEAR SCREENS AND CLOSE PACKAGES
C
	CALL CTERM(2)
	CALL RTERM(2)
	CALL PLOTND
	STOP
	END
C
C
	SUBROUTINE METMAP2(IARRAY,NA,MA,NS,MS,NE,ME,XLL,YLL,XUR,YUR)
C
C	MAKE IMAGE MAP ON META FILE
C
C	AUTHOR: DG LONG, JPL
C	DATE: 25 JAN 1991
C
C	INPUTS:
C	  IARRAY  : COLOR CODES
C	  NA,MA	  : DIMENSIONS OF IARRAY
C	  NS,MS	  : STARTING LOWER-LEFT CORNER ( 0 < NS < NA, 0 < MS < MA)
C	  NE,ME	  : ENDING UPPER-RIGHT CORNER  ( NS <= NS <= NA, MS <= MS <= MA)
C	  	    OF RECTRANGLE TO PLOT (0 < NE-NS, ME-NS < 2048)
C	  XLL,YLL : POSITION OF LOWER-LEFT CORNER OF MAP (IN PLOT UNITS)
C	  XUR,YUR : POSITION OF UPPER-RIGHT CORNER OF MAP (IN PLOT UNITS)
C
C	CALLS:
C	  PPLOT
C	  PPLOTP
C
	INTEGER IARRAY(NA,MA)
C
	COMMON/PXPCOM/LU,MP,RE(2),OX,OY,SF,PX,PY,MX,MW,MCOL,MSC,
     $		PANG,PVP(4)
	SAVE /PXPCOM/
C
C	META FILE LIMITS ON X AND Y IN INCHES
C
	DIMENSION IV(2),AV(2),ALIM(2)
	DATA ALIM/32.7,32.7/
C
	IF (LU.LE.0) RETURN
C
C	ENTER ORIGIN POINT IN METAFILE
C
	CALL PPLOT(XLL,YLL,3)
C
C	ENTER X,Y AXES LENGTHS
C
	XA=XLL
	YA=YLL
	TANG=PANG*.0174532
	AV(1)=XA*COS(TANG)-YA*SIN(TANG)
	AV(2)=XA*SIN(TANG)+YA*COS(TANG)
	AX=SF*AV(1)+OX
	AY=SF*AV(2)+OY
	XA=XUR
	YA=YUR
	TANG=PANG*.0174532
	AV(1)=XA*COS(TANG)-YA*SIN(TANG)
	AV(2)=XA*SIN(TANG)+YA*COS(TANG)
	AV(1)=SF*AV(1)+OX-AX
	AV(2)=SF*AV(2)+OY-AY
C
	DO 720 I1=1,2
		IF (AV(I1).LT.0.0) AV(I1)=0.0
		IF (AV(I1).GT.ALIM(I1)) AV(I1)=ALIM(I1)
		IV(I1)=AV(I1)/RE(I1)
720	CONTINUE
	CALL PPLOTP(IV(1),IV(2),2000)
C
	NX1=NS
	IF (NX1.LT.1) NX1=1
	IF (NX1.GT.2047) NX1=2047
	NX2=NE
	IF (NX2.LT.NX1) NX2=1
	IF (NX2.GT.2048) NX2=2048
	NX=NX2-NX1+1
	NY1=MS
	IF (NY1.LT.1) NY1=1
	IF (NY1.GT.2047) NY1=2047
	NY2=ME
	IF (NY2.LT.NY1) NY2=1
	IF (NY2.GT.2048) NY2=2048
	NY=NY2-NY1+1
	CALL PPLOTP(NX,NY,2001)
C
C	OUTPUT MAP DATA IN Y-MAJOR ORDER, I.E., (1,1) (1,2) (1,3)...
C
	NN=NX*NY
	I2=1
	DO 100 IX=NX1,NX2
	  DO 100 IY=NY1,NY2
		IV(I2)=IARRAY(IX,IY)
		IF (IABS(IV(I2)).GT.32760) IV(I2)=ISIGN(32760,IV(I2))
		I2=I2+1
		IF (I2.EQ.3) THEN
			CALL PPLOTP(IV(1),IV(2),2002)
			I2=1
		ENDIF
100	CONTINUE
	IF (I2.EQ.2) CALL PPLOTP(IV(1),IV(2),2002)
C
	RETURN
	END

