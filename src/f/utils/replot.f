	PROGRAM REPLOTER
C *** LAST REVISED ON 18-MAR-1992 11:40:07.14
C *** SOURCE FILE: [LONGD.GRAPHICS]REPLOT.FOR
C
C	CREATED: DGL 1-NOV-1984
C
C	THIS PROGRAM PERMITS REPLOTTING OF A PRINTER GRAPHICS FILE
C	PRODUCED BY THE LONGLIB GRAPHICS LIBRARY TO A SCREEN DEVICE.
C	THE USER IS PROMPTED TO DETERMINE WHICH SCREEN DEVICE TO USE.
C
C	ANY NEWPAGE COMMANDS IN PRINTER FILE WILL BE PROCESSED AS
C	A CTERM(2) COMMAND FOR THE SCREEN DEVICE.
C	OF COURSE, SINCE THE PRINTER FILE DOES NOT MAINTAIN A COPY OF
C	WHAT DATA WENT TO THE SCREENS, ANY CTERMS USED IN THE ORIGINAL
C	PROGRAM GENERATING THE FILE WILL NOT BE REPEATED BY THIS PROGRAM.
C
C	THIS PROGRAM IS FORTRAN 77 COMPATIBLE WITH THE FOLLOWING EXCEPTIONS;
C		1. TABS (^I) ARE USED TO INDENT LINES
C		2. INTEGER*2 USED FOR UNFORMATTTED FILE IN REGET.
C		   THIS CAN BE REPLACED BY INTEGER IF THIS HAS BEEN DONE
C		   IN THE MAIN LONGLIB PRINTER HISTORY ROUTINE.
C		3. VAX DEPENDENT LIBRAY ROUTINE USED TO GET FILE NAME
C		   FROM COMMAND LINE.
C
	CHARACTER*80 NAME
	SAVE
C
C	VAX DEPENDENT METHOD OF GETTING FILE NAME FROM COMMAND LINE
C	(CAN BE COMMENTED OUT)
C
C	IERR=LIB$GET_FOREIGN(NAME,,IFLAG)
C
C	DEFAULT FILE NAME
C
c        IF (NAME.EQ.' ') NAME='ftn03'
        NAME='fort.3'
cc        NAME='ftn03'
C
C	OPEN LONGLIB WITHOUT METAFILE
C	PROMPT FOR SCREEN TYPE
C
	CALL FRAME(-3,0,0.,0.,1.)
C
C	REPLOT META FILE TO SCREEN
C
	CALL REPLOT(NAME)
C
C	TERMINATE PLOTTING
C
	CALL PLOT(0.,0.,3)
	CALL CTERM(2)
	CALL RTERM(2)
	CALL PLOTND
	STOP
	END
C
C
	SUBROUTINE REPLOT(NAME)
C
C	MAIN ROUTINE TO REPLOT A PRINTER GRAPHICS FILE TO A SCREEN
C	DEVICE FOR THE LONGLIB GRAPHICS LIB.
C
C	NAME	(CHARACTER)	FILE NAME
C
	CHARACTER*(*) NAME
C
	PARAMETER (MAXIM=1310720)
	DIMENSION IAR(MAXIM)
	SAVE
C
C	INITIALIZE
C
	AMX=1.0
	AMY=1.0
	XOLD=0.0
	YOLD=0.0
C
C	OPEN INPUT FILE
C
	MP=999
	OPEN(UNIT=2,FILE=NAME,FORM='UNFORMATTED',STATUS='OLD',
     $		ERR=99) 
C     $, READONLY)
C
C	TOP OF COMMAND READ LOOP
C
1000	CALL REGET(M1,M2,M3,MP,2)
	IF (M3.EQ.11) GOTO 110
	IF (M1.EQ.0.OR.M2.EQ.0) GOTO 110
	SX=1./FLOAT(M1)
	SY=1./FLOAT(M2)
C
C	TOP OF INNER LOOP
C
   1	CONTINUE
	CALL REGET(M1,M2,M3,MP,2)
   2	Y1=FLOAT(M1)*SX
	X1=FLOAT(M2)*SY
C
C	I3 IS THE COMMAND
C
	I3=M3
	IOLD=0
	JOLD=1
C
C	CHECK FOR END OF FILE
C
	IF (M3.GT.999) GOTO 999
C
C	EXECUTE COMMAND
C
	GOTO (10,20,20,10,10,10,10,10,20,100,110) I3
10	GOTO 1
C
C	PLOT COMAND
C
20	CALL PLOT(X1,Y1,I3)
	IF (IABS(I3).EQ.2.OR.IABS(I3).EQ.3) THEN
		XOLD=X1
		YOLD=Y1
	ENDIF
	GOTO 1
C
C	FOR NEWPAGE WE WILL PROMPT FOR SCREEN CLEARS
C
100	CALL NEWPAGE
	CALL CTERM(2)
	CALL CTERM(-1)
	CALL RTERM(2)
	CALL RTERM(-1)
	GOTO 1
999	CONTINUE
	IF (I3.GT.1999) GOTO 1999
	GOTO (1000,1001,1002,1003) I3-999
	GOTO 1
C
C	CHANGE LINE TYPE
C
1001	CONTINUE
	IOLD=M1
	I3=JOLD*10+IOLD+1
	CALL NEWPEN(I3)
	GOTO 1
C
C	PEN COLOR
C
1002	CONTINUE
	IF (M2.GE.0) CALL PLOT(FLOAT(M2),0.,0)
	GOTO 1
C
C	PEN WIDTH
C
1003	CONTINUE
	JOLD=M1
	I3=JOLD*10+IOLD+1
	CALL NEWPEN(I3)
	GOTO 1
C
C	HANDLE IMAGE DATA
C
1999	CONTINUE
C
C	CHECK EACH OPTION
C
	IF (I3.EQ.2000) THEN
		AMX=M1*SX
		AMY=M2*SY
	ENDIF
	IF (I3.EQ.2001) THEN
		INX=M1
		IF (INX.LT.1) INX=1
		INY=M2
		IF (INY.LT.1) INY=1
		NUM=INX*INY
C
C	READ IMAGE DATA INTO AN ARRAY
C		
		IF (MOD(NUM,2).EQ.1) NUM=NUM+1
		NUM=NUM/2
		IC=0
C
C	READ IN MAP DATA IN Y-MAJOR ORDER, I.E., (0,0) (1,0) (2,0)...
C
		DO 2002 I=1,NUM
			I1=(I-1)*2+1
			CALL REGET(M1,M2,M3,MP,2)
			IF (M3.NE.2002) GOTO 2
			IF (I1.LT.MAXIM) THEN
				I2=1+(I1-1)/INY+MOD(I1-1,INY)*INX
				IAR(I2)=M1
				IY=MOD(I1,INY)
				I2=1+I1/INY+MOD(I1,INY)*INX
				IAR(I2)=M2
			ENDIF
2002		CONTINUE
C
C	DISPLAY IMAGE ARRAY
C
CC		CALL METMAP(IAR,INX,INY,INX,INY,XOLD,YOLD,AMX+XOLD,AMY+YOLD)
C
C	TEKTRONICS TERMINAL
C
		CALL TERSCL(1,IT,XOLD,YOLD,IXT,IYT)
		IF (IT.GT.0) THEN		! IF PROPER TEK TERMINAL
		  CALL TERSCL(1,IT,AMX+XOLD,AMY+YOLD,IX1,IY1)
		  RJXT=(FLOAT(IX1-IXT)/FLOAT(INX))
		  RJYT=(FLOAT(IY1-IYT)/FLOAT(INY))
		  CALL TEKMAP(IAR,INX,INY,INX,INY,IXT,IYT,RJXT,RJYT)
		ENDIF
C
C	RAMTEK
C
		ICHAN=IRMCHAN(ID)
		IF (ICHAN.GT.0) THEN
		  CALL RMPIX(XOLD,YOLD,IXR,IYR)
		  CALL RMPIX(AMX+XOLD,AMY+YOLD,IX1,IY1)
		  RJX=FLOAT(IX1-IXR)/FLOAT(INX)
		  RJY=FLOAT(IY1-IYR)/FLOAT(INY)
		  CALL RAMMAP(IAR,INX,INY,INX,INY,IXR,IYR,RJX,RJY)
		ENDIF
C
	ENDIF
	GOTO 1
C
110	CLOSE(2)
 	RETURN
 99     CONTINUE
        CALL CTERM(1)
        WRITE (*,*) '*** input file not found! ***',name
        RETURN
	END
C
	SUBROUTINE REGET(M1,M2,M3,MP,ILU)
C
C	READ DATA FROM PRINTER DATA FILE
C	
	INTEGER*2 M(128)
	SAVE
	MP=MP+3
	IF (MP.GT.128) THEN
	        !print *,'reget',MP
		READ (ILU,ERR=99,END=99) M
		MP=3
	ENDIF
	M3=M(MP)
	M2=M(MP-1)
	M1=M(MP-2)
	!print *,MP,':',M3,M2,M1
	IF (M3.EQ.999) GOTO 99
	RETURN
99	M3=11
	RETURN
	END
