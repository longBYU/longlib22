C
	SUBROUTINE LINEOUTF(RX1,RY1,RX2,RY2,RES,
     $		IXWIDE,IYWIDE,IBNT,IADD,XX)
	DIMENSION IADD(1),XX(1)
C
C	COMPUTE THE LIST OF PIXELS (LEXICOGRAPHIC ORDER) A ALONG THE
C	LINE FROM (RX1,RY1) TO (RX2,RY2) WITH ENHANCED RESOLUTION
C
C	RX1,RY1	(R)	START POINT OF LINE SEGMENT
C	RX2,RY2	(R)	STOP POINT OF LINE SEGMENT
C	RES	(R)	RESOLUTION ENHANCEMENT FACTOR
C	IXWIDE	(I)	X DIMENSION (0 <= IX <= IXWIDE-1)
C	IYWIDE	(I)	Y DIMENSION (0 <= IY <= IYWIDE-1)
C	IBNT	(I)	RETURNED NUMBER OF PIXELS
C	IADD	(I)	RETURNED ARRAY OF PIXEL INDEXES
C	XX	(R)	DISTANCE OF PIXEL FROM START
C
C	LEXICOGRAPHIC INDEX IS: N=IY*IXWIDE+IX+1
C
	INTEGER DX,DY,X,Y,X1,Y1,X2,Y2,DIF,D
	LOGICAL INV
C
	DX=NINT(ABS((RX1-RX2)*RES))
	DY=NINT(ABS((RY1-RY2)*RES))
	X1=NINT(RX1*RES)
	X2=NINT(RX2*RES)
	Y1=NINT(RY1*RES)
	Y2=NINT(RY2*RES)
	IX0=NINT(RX1*RES)
	IY0=NINT(RY1*RES)
	IF (X1.LT.0) X1=0
	IF (X2.LT.0) X2=0
	IF (Y1.LT.0) Y1=0
	IF (Y2.LT.0) Y2=0
	IF (X1.GE.IXWIDE*RES) X1=int(IXWIDE*RES-1)
	IF (X2.GE.IXWIDE*RES) X2=int(IXWIDE*RES-1)
	IF (Y1.GE.IYWIDE*RES) Y1=int(IYWIDE*RES-1)
	IF (Y2.GE.IYWIDE*RES) Y2=int(IYWIDE*RES-1)
	INV=.FALSE.
	IF (DX.LT.DY) THEN
		X=X1
		X1=Y1
		Y1=X
		X=X2
		X2=Y2
		Y2=X
		X=DY
		DY=DX
		DX=X
		INV=.TRUE.
		I1=IX0
		IX0=IY0
		IY0=I1
	ENDIF
	IF (X2.LT.X1) THEN
		X=X1
		X1=X2
		X2=X
		Y=Y1
		Y1=Y2
		Y2=Y
	ENDIF
	DIF=1
	IF (Y2.LT.Y1) DIF=-1
	D=2*DY-DX
	I1=2*DY
	I2=2*(DY-DX)
	Y=Y1
	IYY=0
C
	IXW=IXWIDE
	IBNT=0
	DO 10 X=X1,X2
		IBNT=IBNT+1
		IF (INV) THEN
			JX=NINT((X+IYY)/RES)
			JY=NINT(Y/RES)
		ELSE
			JX=NINT((Y+IYY)/RES)
			JY=NINT(X/RES)
		ENDIF
		INDEX=JX*IXW+JY+1
		IADD(IBNT)=INDEX
		XX(IBNT)=(FLOAT(X-IX0)**2+FLOAT(Y-IY0)**2)/RES**2
C		XX(IBNT)=(X-IX0)**2+(Y-IY0)**2
		IF (XX(IBNT).GT.0.0) XX(IBNT)=SQRT(XX(IBNT))
		IF (D.LT.0) THEN
			D=D+I1
		ELSE
			D=D+I2
			Y=Y+DIF
		ENDIF
10	CONTINUE
	RETURN
	END



