C
C *** LAST REVISED ON 10-AUG-1990 14:59:30.54
C *** SOURCE FILE: [DL.GRAPHICS.LONGLIB77]AUXLIB.FOR
C
C *************************************************************************
C
C	SECTION 2 OF LONGLIB GRAPHICS LIBRARY PLOT ROUTINES.
C	THESE ROUTINES ARE DEVICE INDEPENDENT ROUTINES.
C
C	THIS FILE IS COMPATIBLE WITH THE ANSI FORTRAN 77 SPECIFICATION
C	WITH THE FOLLOWING EXCEPTIONS:
C		1. TABS (^I) ARE USED TO INDENT LINES
C		2. COMMENT LINES EXTEND FROM SOURCE LINES WITH THE
C		   SEPARATOR !.
C		3. INTEGER*2 IS USED TO SAVE SPACE IN "SYMBOL"
C
C *************************************************************************
C
	SUBROUTINE AXIS(X0,Y0,A0,N0,S0,T0,C0,D0,NM,ML,ICOL)
C
C	EXTENSIVELY MODIFIED BY D.LONG   7-OCT-83	AT JPL
C	REVISED 13-AUG-1987 DGL
C	+ IMPROVED APPEARANCE OF EXPONENT, ADDED EXPONENT COLOR
C
C	X0  (R)	X COORDINATE OF START OF AXIS
C	Y0  (R)	Y COORDINATE OF START OF AXIS
C	A0  (C)	CHARACTER STRING TO DESCRIBE AXIS
C	N0  (I)	NUMBER OF CHARACTERS IN STRING
C		- ON CLOCKWISE SIDE OF AXIS (NORMAL FOR X)
C		+ ON COUNTER CLOCKWISE SIDE OF AXIS (NORMAL FOR Y)
C		HUNDREDS DIGIT	= 1 NO LABELING OF AXIS--TICKS AND LINE ONLY
C		THOUSANDS DIGIT = 1 HORIZONTAL NUMBERS IN Y AXIS LABEL
C		10 THOUSANDS DIGIT=1 USE NM,ML PARAMETERS
C		100 THOUSANDS DIGIT=1 USE COLOR PARAMTERS
C	S0  (I)	LENGTH OF AXIS (SHOULD BE INTEGER-VALUED)
C		< 0	TICKS PLACED ON OPPOSITE SIDE OF AXIS LABEL
C		= 0	NO ACTION
C		> 0	NORMAL
C	T0  (R)	ANGLE OF AXIS TO X AXIS OF PLOT COORDINATE SYSTEM
C		0.0 FOR X-AXIS
C		90.0 FOR Y-AXIS
C	C0  (R)	COORDINATE OF MINIMUM TICK ON AXIS
C	D0  (R)	SCALING DISTANCE BETWEEN "1" TICKS
C		NOTE:  THE FOLLOWING ARE ACCESSED ONLY IF ABS(NO)>10000
C	NM  (I)	NUMBER OF MINOR AXIS TICKS BETWEEN MAJOR TICKS (DEFAULT=0)
C	ML  (I)	NUMBER OF MAJOR AXIS TICKS (DEFAULT= 1 TICK/INCH)
C	ICOL (I) COLOR ARRAY (ACCESSED ONLY IF ABS(NO)>100000
C		 ICOL(1) AXIS COLOR
C		 ICOL(2) NUMBER COLOR
C		 ICOL(3) LABEL COLOR
C		 ICOL(4) EXPONENT COLOR
C		PEN COLOR ON RETURN DEPENDS ON LAST ITEM PLOTTED
C		IN THE SEQUENCE INDICATED
C
	CHARACTER*(*) A0(1)
	INTEGER ICOL(4)
	LOGICAL VERT,TICKS,COLOR
C
	CS=0.15			! CHARACTER SIZE
	IF (S0.EQ.0.0) GOTO 200	! ZERO LENGTH AXIS
	VERT=.FALSE.		! NO VERTICAL NUMBERS ON HORIZONTAL AXIS
	TICKS=.TRUE.		! PUT ON TICKS
	HOR=T0
	T5=0.1			! TICK LENGTH
	B7=T5+.08		! NUMBER DISTANCE FROM AXIS
	B6=B7
	B8=0.0
	NM1=0				! NUMBER MINOR TICKS
	N2=int(ABS(S0)+0.5)		! NUMBER OF MAJOR TICKS
	S1=FLOAT(N2)
	XL=1.				! INCREMENT BETWEEN MAJOR TICKS
	N1=IABS(N0)
	COLOR=.FALSE.
	IF (N1.GE.100000) THEN
		N1=MOD(N1,100000)	! USE COLOR ARRAY
		COLOR=.TRUE.
	ENDIF
	IF (N1.GE.10000) THEN
		N1=MOD(N1,10000)
		N2=IABS(ML)		! NUMBER MAJOR TICKS
		IF (N2.EQ.0) N2=1
		S1=ABS(S0)
		XL=ABS(S0)/FLOAT(N2)	! SPACING MAJOR TICKS
		NM1=IABS(NM)+1		! NUMBER MINOR TICKS
		XM=XL/FLOAT(NM1)	! SPACING MINOR TICKS
	else
	   XM=0.
	ENDIF
	IF(N0.LT.0)GOTO 10
	B3=CS*3.8			! COUNTER-CLOCKWISE LABELING
	B4=CS+0.08
	T2=T0
	GOTO 20
10	B3=-CS*4.			! CLOCKWISE LABELING
	B4=-T5-CS-.05
	T2=T0
	T5=-T5
20	IF (N1.GE.1000)THEN
		N1=MOD(N1,1000) 	! VERTICAL NUMBERS ON HORIZONTAL AXIS
		VERT=.TRUE.
		HOR=0.0
		B4=(ABS(T5)*(1.+SIGN(1.,S0))/2.+.1)*SIGN(1.,FLOAT(N0))
		B6=.49*CS
	ENDIF
	IF (N1.GE.100) THEN
		N1=MOD(N1,100)		! NO TICKS
		TICKS=.FALSE.
	ENDIF
	T5=T5*SIGN(1.,S0)
	T1=T0*0.017453294
	T3=COS(T1)
	T4=SIN(T1)
C
	T6=T5*T3
	T5=T5*T4
	X1=X0
	Y1=Y0
	IF (COLOR) CALL PLOT(FLOAT(ICOL(1)),0.,0) ! COLOR
	DO 100 I=1,N2				! MAJOR TICKS
	IF (NM1.EQ.0) GOTO 106
		DO 105 K=1,NM1			! DO MINOR TICKS
		X2=X1+T3*FLOAT(K-1)*XM
		Y2=Y1+T4*FLOAT(K-1)*XM
		X3=X2-T5*.5
		Y3=Y2+T6*.5
		CALL PLOT(X2,Y2,3)
105		CALL PLOT(X3,Y3,2)
106	X2=X1-T5
	Y2=Y1+T6
	CALL PLOT(X2,Y2,3)
	CALL PLOT(X1,Y1,2)
	X1=X1+T3*XL
	Y1=Y1+T4*XL
	IF (T0.EQ.90.) X1=X0
100	CALL PLOT(X1,Y1,2)
	X2=X1-T5
	Y2=Y1+T6
	CALL PLOT(X2,Y2,2)			! FINISH LAST MAJOR TICK
C	CHECK FOR EXPONENT VALUE
	D1=D0					! SCALING FACTOR
	C1=C0+S1*D1				! STARTING VALUE
	E1=0.0					! EXPONENT
	IF(D1.EQ.0.0)GOTO 140
110	IF(ABS(D1).LT.10.0)GOTO 130
	D1=D1*0.1
	C1=C1*0.1
	E1=E1+1.0
	GOTO 110
120	D1=D1*10.0
	C1=C1*10.0
	E1=E1-1.0
130	IF(ABS(D1).LT.0.5)GOTO 120
140	CONTINUE			! PEN AT END OF AXIS
	IF (.NOT.TICKS) THEN
		IF (COLOR) CALL PLOT(FLOAT(ICOL(3)),0.,0) ! COLOR
		GOTO 200
	ENDIF
	IF (VERT) THEN
		C2=C1-N2*D1		! MAKE SPACE FOR VERTICAL NUMBERS
		IC=1			! ON HORIZONTAL AXIS
		IF (ABS(C2).GE.1.0) IC=IFIX(ALOG10(ABS(C2)))
		IC2=1
		IF (ABS(C1).GE.1.0) IC2=IFIX(ALOG10(ABS(C1)))
		NC1=MAX(IC,IC2)+2
		IF (C2.LT.0.0.OR.C0.LT.0.0) NC1=NC1+1
		IF (N0.GT.0.0) B4=B4+FLOAT(NC1)*CS
		B3=0.0
		B8=(.25+ABS(T5)*(SIGN(1.,S0)+1.)/2.+FLOAT(NC1)*CS)
     $			*SIGN(1.,FLOAT(N0))
	ENDIF
	X2=X1-B4*T4-B7*T3		! LOCATE CENTER NUMBER LABELS
	Y2=Y1+B4*T3-B6*T4
	N2=N2+1
	IF (COLOR) CALL PLOT(FLOAT(ICOL(2)),0.,0) ! COLOR
	DO 150 I=1,N2			! LABEL MAJOR TICKS
		CALL NUMBER(X2,Y2,CS,C1,HOR,0.01,-1)
		C1=C1-D1*S1/FLOAT(N2-1)
		X2=X2-T3*XL
		Y2=Y2-T4*XL
150	CONTINUE
	IF (COLOR) CALL PLOT(FLOAT(ICOL(3)),0.,0) ! COLOR
	IF (N1.GT.0) THEN		! ADD TITLE
		C2=0.0
		Y2=0.0
		CALL SYMBOL(C2,Y2,CS,A0,0.0,N1,-3) ! TITLE LENGTH
		B1=0.5*(ABS(S0)-C2)		! CENTER TITLE
		IF (E1.NE.0.) B1=B1-CS*3.	! PUT ON EXPONENT
		X2=X0+B1*T3-B3*T4-B8*T4
		Y2=Y0+B1*T4+B3*T3
		CALL SYMBOL(X2,Y2,CS,A0,T2,N1,-1)
	ELSE
		C2=0.0
		B1=0.5*ABS(S0)
		X2=X0+B1*T3-B3*T4-B8*T4
		Y2=Y0+B1*T4+B3*T3
	ENDIF
	IF (E1.EQ.0.0) GOTO 200		! NO EXPONENT
	IF (COLOR) CALL PLOT(FLOAT(ICOL(4)),0.,0) ! COLOR
	C2=C2+CS
	X2=X2+C2*T3
	Y2=Y2+C2*T4
	CALL SYMBOL(X2,Y2,CS,'(X10',T2,4,-1)
	X2=X2+CS*3.75*T3-CS*T4*0.4
	Y2=Y2+CS*3.75*T4+CS*T3*0.4
	CALL NUMBER(X2,Y2,CS,E1,T2,0.0,-1)
	B2=0.8+AINT(ALOG10(ABS(E1)))
	IF (E1.LT.0.0) B2=B2+1
	X2=X2+B2*CS*T3+CS*T4*0.4
	Y2=Y2+B2*CS*T4-CS*T3*0.4
	CALL SYMBOL(X2,Y2,CS,')',T2,1,-1)
200	RETURN
	END
