C
C
	SUBROUTINE NUMBER(X,Y,HGHT,Z,T,F0,IPF)
C
C	WRITTEN BY D. LONG    AUG, 1983 AT JPL
C	REVISED:	JUNE 1990
C	
C	PLOTS THE FLOATING POINT NUMBER Z (CAN PLOT AS AN INTEGER)
C
C	X,Y	(R) COORDINATES OF STRING
C		    (999.,999.) CONTINUE FROM LAST POINT
C	HGHT	(R) HEIGHT OF THE PLOTTED NUMBER
C	Z	(R) FLOATING POINT NUMBER TO BE PLOTTED
C	T	(R) ORIENTATION ANGLE
C	F0	(R) PLOTTING FORMAT (Fn.j)
C		  n = TOTAL NUMBER OF SPACES TO USE (INCLUDING SIGN AND D.P.)
C			[MAX 18 CHARACTERS WIDE]
C		  j = DIGITS TO RIGHT OF DECIMAL POINT (TWO DIGITS EXPECTED)
C			(F4.2 SHOULD BE WRITTEN F4.02)
C		  IF F0 < 0 USE EXPONENTIAL NOTATION (I.E., En,j)
C		  F0 = 1 PLOT IN FLOATING POINT FREE FORMAT
C		  F0 = 0 PLOT INTEGER PORTION WITH NO D.P. (FREE FORMAT)
C		  F0 =-1 PLOT IN EXPONENTIAL FREE FORMAT
C			 NOTE: FREE FORMATS HAVE LEADING SPACES SUPPRESSED
C		  F0 > 1000 PLOT INTEGER PORTION IN FIXED FORMAT WITH
C			n DIGITS AND WITHOUT D.P.
C		  IF n=0 THEN PLOT INTEGER PORTION, DECIMAL POINT, AND
C			 j DIGITS TO RIGHT OF DECIMAL POINT
C		  WHEN Z OVERFLOWS THIS FORMAT, SPACE IS FILLED WITH ASTERICKS
C	IPF	(I) NUMBER CENTERING FLAG (SEE SYMBOL)
C		=-3 X,Y ARE LOWER LEFT CORNER, END OF STRING RETURNED IN X,Y
C			BUT NUMBER IS NOT PLOTTED
C		=-2 X,Y ARE LOWER LEFT CORNER, END OF STRING RETURNED IN X,Y
C		=-1 X,Y ARE LOWER LEFT CORNER
C		= 0 X,Y ARE STRING CENTER
C		=+1 X,Y ARE LOWER RIGHT CORNER
C		=+2 NO PLOT OUTPUT
C
	CHARACTER B*18, FB*8, FB1*8		! WORKING BUFFERS
C
	IFF=0
	HG=HGHT
	IF (HG.EQ.0.0) HG=0.15
	T1=T
    	ND=0
	NN=0
	FA=F0
	IF (ABS(FA).GT.1022.0) FA=0.0
	IF (FA.EQ.0.0) GOTO 10		! INTEGER FORMAT
	IF (FA.GT.999.0) THEN		! PLOT FORMATTED INTEGER
		NN=int(AMOD(FA,1000.))
		FA=0.0
	ELSE				! PLOT FLOAT OR EXPON NUMBER
		F=int(ABS(FA)*1.000002)
		NN=int(F)
		F=(F-NN)*100.
		ND=int(F)
	ENDIF
 10	IF (ND.GT.17) ND=ND/10		! CORRECT SIMPLE INPUT ERRORS
	IF (NN.EQ.0) THEN		! DIGITS TO LEFT OF DECIMAL POINT
		NN=ND+2
		IF (Z.EQ.0.AND.FA.EQ.0.0) NN=1
		IF (Z.NE.0.0) THEN
			ALG=ALOG10(ABS(Z))
			IF (ALG.LT.0.0) ALG=0.0
			NN=ND+2+int(ALG)
			IF (FA.EQ.0.0) NN=1+int(ALG)
		ENDIF
		IF (Z.LT.0.0) NN=NN+1
		IF (FA.LT.0.0) NN=NN+4
	ENDIF
	IF (ND.GT.NN) GOTO 90		! FORMAT ERROR
	IF (NN.GT.18) NN=18		! MAX CHARACTERS
	IF (FA.EQ.0.0) THEN		! INTEGER
		I=int(Z)
		FB=CHAR(NN-10*(NN/10)+48)//')'
		FB1=FB
		IF (NN/10.GT.0) FB=CHAR(NN/10+48)//FB1(1:7)
		FB1='(I'//FB(1:6)
		WRITE(B,FB1,ERR=90) I
	ELSE				! FLOATING POINT OR EXPONENTIAL
		IF (NN.GT.1) THEN
			FB=CHAR(ND-10*(ND/10)+48)//')'
			FB1=FB
			IF (ND/10.GT.0) FB=CHAR(ND/10+48)//FB1(1:7)
			FB1=CHAR(NN-10*(NN/10)+48)//'.'//FB(1:6)
			FB=FB1
			IF (NN/10.GT.0) FB=CHAR(NN/10+48)//FB1(1:7)
			IF (FA.GT.0.0) THEN
				FB1='(F'//FB(1:6)
			ELSE
				FB1='(E'//FB(1:6)
			ENDIF
		ELSE
			IF (FA.GT.0.0) THEN
				FB1='(F)'
			ELSE
				FB1='(E)'
			ENDIF
			NN=16
			IFF=1
		ENDIF
		WRITE(B,FB1,ERR=90) Z
		IF (IFF.EQ.1) THEN		! REMOVE LEADING SPACES
			DO 48 I=1,18
				IF (B(1:1).EQ.' ') B=B(2:18)
 48			CONTINUE
		ENDIF
	ENDIF
 50	CALL SYMBOL(X,Y,HG,B,T1,NN,IPF)
	RETURN
 90	DO 95 I=1,18
		B(I:I)='*'
		IF (I.EQ.NN-ND) B(I:I)='.'
 95	CONTINUE
	GOTO 50
	END
