C
C
	SUBROUTINE PLRLN(R,T,N,J,L,IR,RMIN,DR)
C
C	CREATED BY D. LONG 8-AUG-83	AT JPL
C
C	DRAWS A CONNECTED AND/OR SYMBOLED LINE WHEN POINTS ARE SPECIFIED
C	IN POLAR COORDINATES.
C
C	R	(R) ARRAY OF UNSCALED RADIUS VALUES
C	T	(R) ARRAY OF UNSCALED ANGLE VALUES IN DEGREES
C	N	(I) NUMBER OF POINTS IN THE ARRAY
C	J	(I) >0, SYMBOL AND LINE.  SYMBOLS PLOTTED EVERY JTH POINT
C		    =0, LINE ONLY
C		    <0, SYMBOL ONLY. SYMBOLS PLOTTED EVERY JTH POINT
C	L	(I) NUMBER OF SYMBOL ,SEE SYMBOL ROUTINE FOR LIST
C	IR	(I) FIRST REVALENT VALUE IN ARRAYS
C	RMIN	(R) MINIMUM VALUES IN R ARRAY (DETERMINED BY SCALE)
C	DR	(R) SCALED (SMOOTHED) INCREMENT (DETERMINED BY SCALE)
C		    IF DR=0.0, THEN PLOTTED R IS ZERO
C
	REAL R(1),T(1)
	K=1
	TPI=3.141592654/180.
	IF (N.LT.1) GOTO 110
	LA=L
	IF (LA.LT.0.OR.LA.GT.117) LA=100
	I2=IR
	I3=3
	IF(J.GE.0)I3=2
	I4=3
	I5=N-IR+1
100	IF (DR.NE.0.0) THEN
		R1=(R(I2)-RMIN)/DR
	ELSE
		R1=0.0
	ENDIF
	T1=T(I2)*TPI
	X1=R1*COS(T1)
	Y1=R1*SIN(T1)
	CALL PLOT(X1,Y1,I4)
	I4=I3
	IF(J.NE.0)THEN
		IF (MOD((I2),J*K).EQ.0) CALL SYMBOL(X1,Y1,
     $			0.14,CHAR(LA),0.0,-1,-1)
	ENDIF
	I2=I2+K
	I5=I5-K
	IF(I5.GT.0)GOTO 100
110	RETURN
	END
