C
C *** LAST REVISED ON 26-AUG-1994 21:35:48.77
C *** SOURCE FILE: [LONGLIB93.SOURCES.FORTRAN.3DLIB]INIT3D.FOR
C
C **************************************************************************
C
C	LONGLIB3D LIBRARY ROUTINES FOR 3-D PLOTTING WITHOUT HIDDEN
C	LINE REMOVAL.
C
C **************************************************************************
C
	SUBROUTINE INIT3D(X,Y,Z,XA,YA,ZA,THETA,DDS,SSF,II3D)
C
C	CREATED: DGL 1-AUG-1984
C
C	INITIALIZE 3D PLOTTING ROUTINE
C
C	X,Y,Z	 (R):	VIEWING POINT
C	XA,YA,ZA (R):	ORIGIN POINT (LOOKING AT FROM VIEWING POINT)
C	THETA	 (R):	ROTATION ABOUT AXIS DEFINED BY (X,Y,Z), (XA,YA,ZA) (DEG)
C	DDS	 (R):	PERSPECTIVE SCALE
C	SSF	 (R):	RELATIVE SCALE FACTOR
C	II3D	 (I):	PLOTTING ON FLAG (-1 = OFF)
C
	REAL LAST(4),V0(4),VORG(4),ROT(4,4),VIEW(4,4),TEMP1(4,4)
	COMMON /CPLOT3D/LAST,V0,VORG,ROT,DS,VIEW,TEMP1,SF,I3D,ZCLIP,IZCLIP
	SAVE /CPLOT3D/
C
	IZCLIP=0
	I3D=II3D
	SF=1.0
	IF (SSF.GT.0.0) SF=SSF
	DS=DDS
	IF (DDS.LT.0.0) DS=2.
	DO 20 I=1,4
		DO 10 J=1,4
			ROT(I,J)=0.0
			TEMP1(I,J)=0.0
10			VIEW(I,J)=0.0
		ROT(I,I)=1.0
		TEMP1(I,I)=1.0
		VIEW(I,I)=1.0
		VORG(I)=0.0
		V=0.0
20	CONTINUE
	VIEW(4,1)=-X			! SHIFT ORIGIN TO VIEW POINT
	VIEW(4,2)=-Y
	VIEW(4,3)=-Z
	D=SQRT((X-XA)**2+(Y-YA)**2+(Z-ZA)**2)
	VV=SQRT((Y-YA)**2+(X-XA)**2)
	TEMP1(2,2)=0.0
	TEMP1(3,3)=0.0
	TEMP1(3,2)=1.0
	TEMP1(2,3)=-1.0
	TEMP1(1,1)=-1.0
	CALL MATMUL4(VIEW,VIEW,TEMP1)	! REVERSE Z COORIDINATE
	IF (VV.NE.0.0) THEN
		TEMP1(1,1)=(Y-YA)/VV
		TEMP1(2,2)=1.0
		TEMP1(3,3)=TEMP1(1,1)
		TEMP1(2,3)=0.0
		TEMP1(3,2)=0.0
		TEMP1(3,1)=-(X-XA)/VV
		TEMP1(1,3)=-TEMP1(3,1)
		CALL MATMUL4(VIEW,VIEW,TEMP1)	! ROTATE ABOUT Y AXIS
	ENDIF
	IF (D.NE.0.0) THEN
		TEMP1(3,1)=0.0
		TEMP1(1,3)=0.0
		TEMP1(1,1)=1.0
		TEMP1(2,2)=VV/D
		TEMP1(2,3)=-(Z-ZA)/D
		TEMP1(3,2)=-TEMP1(2,3)
		TEMP1(3,3)=TEMP1(2,2)
		CALL MATMUL4(VIEW,VIEW,TEMP1)	! ROTATE ABOUT X AXIS
	ENDIF
	IF (THETA.NE.0.0) THEN
		CALL ROTEM(3,THETA,TEMP1)
		CALL MATMUL4(VIEW,VIEW,TEMP1)
	ENDIF
	DO 50 I=1,4
		DO 40 J=1,4
			TEMP1(I,J)=0.0
40		CONTINUE
		TEMP1(I,I)=1.0
50	CONTINUE
	IF (DS.GT.0.0) THEN
		TEMP1(1,1)=DS
		TEMP1(2,2)=DS
		CALL MATMUL4(VIEW,VIEW,TEMP1)	! xN  CLIPPING WINDOW
	ENDIF
	RETURN
	END
