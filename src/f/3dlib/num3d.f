C
C
	SUBROUTINE NUM3D(XA,YA,ZA,PA,PB,PG,HGHT,Z,F0)
C
C	CREATED: DGL 1-OCT-1984
C	
C	PLOTS A FLOATING POINT NUMBER IN 3-D USING SYM3D
C
C	XA,YA,ZA COORDINATES OF STRING LOWER-LEFT CORNER
C		  (999.,999.,999.) CONTINUE FROM LAST POINT
C	PA,PB	SPECIFIES THE PROJECTED ANGLES (IN DEGS) RELATIVE TO THE
C		 X-Y PLANE AND THE X-Z PLANE OF THE LINE ALONG THE
C		 BASE OF THE PLOTTED NUMBER SEQUENCE
C	PG	SPECIFIES THE ROTATION OF THE PLOTTED SYMBOLS AROUND
C		THE LINE SPECIFIED BY PA,PB
C	HGHT	HEIGHT OF THE PLOTTED NUMBER
C	Z	FLOATING POINT NUMBER TO BE PLOTTED
C	F0	PLOTTING FORMAT (Fn.j)
C		  n = TOTAL NUMBER OF SPACES TO USE (INCLUDING SIGN AND D.P.)
C			[MAX 18 CHARACTERS WIDE]
C		  j = DIGITS TO RIGHT OF DECIMAL POINT (TWO DIGITS EXPECTED)
C			(F4.2 SHOULD BE WRITTEN F4.02)
C		  IF F0 < 0 USE EXPONENTIAL NOTATION (I.E., En,j)
C		  F0 = 1 PLOT IN FLOATING POINT FREE FORMAT
C		  F0 = 0 PLOT INTEGER PORTION WITH NO D.P. (FREE FORMAT)
C		  F0 =-1 PLOT IN EXPONENTIAL FREE FORMAT
C			 NOTE: FREE FORMATS HAVE LEADING SPACES SUPPRESSED
C		  F0 > 1000 PLOT INTEGER PORTION IN FIXED FORMAT WITH
C			n DIGITS AND WITHOUT D.P.
C		  IF n=0 THEN PLOT INTEGER PORTION, DECIMAL POINT, AND
C			 j DIGITS TO RIGHT OF DECIMAL POINT
C		  WHEN Z OVERFLOWS THIS FORMAT, SPACE IS FILLED WITH ASTERICKS
C
	CHARACTER B*18, FB*8		! WORKING BUFFERS
C
	XT=XA
	YT=YA
	ZT=ZA
	PAT=PA
	PBT=PB
	PGT=PG
	IFF=0
	HG=HGHT
	IF (HG.EQ.0.0) HG=0.15
	T=0.0  ! orientation angle
	T1=T
    	ND=0
	NN=0
	FA=F0
	IF (ABS(FA).GT.1022.0) FA=0.0
	IF (FA.EQ.0.0) GOTO 10		! INTEGER FORMAT
	IF (FA.GT.999.0) THEN		! PLOT FORMATTED INTEGER
		NN=int(AMOD(FA,1000.))
		FA=0.0
	ELSE				! PLOT FLOAT OR EXPON NUMBER
		F=ABS(FA)*1.000002
		NN=int(F)
		F=(F-NN)*100.
		ND=int(F)
	ENDIF
 10	IF (ND.GT.17) ND=ND/10		! CORRECT SIMPLE INPUT ERRORS
	IF (NN.EQ.0) THEN		! DIGITS TO LEFT OF DECIMAL POINT
		NN=ND+2
		IF (Z.EQ.0.AND.FA.EQ.0.0) NN=1
		IF (Z.NE.0.0) THEN
			ALG=ALOG10(ABS(Z))
			IF (ALG.LT.0.0) ALG=0.0
			NN=ND+2+int(ALG)
			IF (FA.EQ.0.0) NN=1+int(ALG)
		ENDIF
		IF (Z.LT.0.0) NN=NN+1
		IF (FA.LT.0.0) NN=NN+4
	ENDIF
	IF (ND.GT.NN) GOTO 90		! FORMAT ERROR
	IF (NN.GT.18) NN=18		! MAX CHARACTERS
	IF (FA.EQ.0.0) THEN		! INTEGER
		I=int(Z)
		FB=CHAR(NN-10*(NN/10)+48)//')'
		IF (NN/10.GT.0) FB=CHAR(NN/10+48)//FB(1:7)
		FB='(I'//FB(1:6)
		WRITE(B,FB,ERR=90) I
	ELSE				! FLOATING POINT OR EXPONENTIAL
		IF (NN.GT.1) THEN
			FB=CHAR(ND-10*(ND/10)+48)//')'
			IF (ND/10.GT.0) FB=CHAR(ND/10+48)//FB(1:7)
			FB=CHAR(NN-10*(NN/10)+48)//'.'//FB(1:6)
			IF (NN/10.GT.0) FB=CHAR(NN/10+48)//FB(1:7)
			IF (FA.GT.0.0) THEN
				FB='(F'//FB(1:6)
			ELSE
				FB='(E'//FB(1:6)
			ENDIF
		ELSE
			IF (FA.GT.0.0) THEN
				FB='(F)'
			ELSE
				FB='(E)'
			ENDIF
			NN=16
			IFF=1
		ENDIF
		WRITE(B,FB,ERR=90) Z
		IF (IFF.EQ.1) THEN		! REMOVE LEADING SPACES
			DO 48 I=1,18
				IF (B(1:1).EQ.' ') B=B(2:18)
 48			CONTINUE
		ENDIF
	ENDIF
 50	CALL SYM3D(XT,YT,ZT,PAT,PBT,PGT,HG,B,NN)
 	RETURN
 90	DO 95 I=1,18
		B(I:I)='*'
		IF (I.EQ.NN-ND) B(I:I)='.'
 95	CONTINUE
	GOTO 50
	END
