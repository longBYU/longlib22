C
C *** LAST REVISED ON 26-AUG-1994 21:55:11.40
C *** SOURCE FILE: [LONGLIB93.SOURCES.FORTRAN.3DLIB]PLOT3D.FOR
C
	SUBROUTINE PLOT3D(X,Y,Z,ID)
C
C	CREATED: DGL 1-AUG-1984
C
C	PLOTS A 3D LINE WITH PERSPECTIVE
C
C	X,Y,Z	(R):	POINT LOCATION
C	ID	(I):	PLOT COMMAND
C			 0 = CHANGE COLOR (X), AND ANGLE (Y) OF PLOT PACKAGE
C			 1 = CHANGE RELATIVE ROTATION MATRIX
C			-1 = CHANGE RELATIVE SCALE FACTOR BY X
C			-2 = PLOT TO X,Y,Z PEN DOWN NEW ORG
C			 2 = PLOT TO X,Y,Z PEN DOWN
C			-3 = MOVE TO X,Y,Z PEN UP NEW ORG
C			 3 = MOVE TO X,Y,Z PEN UP
C			 4 = SET TRANSFORMED Z-PLANE CLIP TO (X,Y,Z)
C			-4 = UNSET TRANSFORMED Z-PLANE CLIP
C			-9 = ERASE TO X,Y,Z PEN DOWN NEW ORG
C			 9 = ERASE TO X,Y,Z PEN DOWN
C
	REAL LAST(4),V0(4),VORG(4),ROT(4,4),VIEW(4,4),TEMP1(4,4)
	COMMON /CPLOT3D/LAST,V0,VORG,ROT,DS,VIEW,TEMP1,SF,I3D,ZCLIP,IZCLIP
	SAVE /CPLOT3D/
	DIMENSION V1(4),V2(4),V3(4)
C
	V1(1)=X
	V1(2)=Y
	V1(3)=Z
	V1(4)=1.0
	CALL MTV4(V1,ROT,V1)	! ROTATE AROUND LOCAL ORIGIN
	V2(1)=(V1(1)-VORG(1))*SF! SHIFT ORIGIN TO WORLD COORINDATES
	V2(2)=(V1(2)-VORG(2))*SF
	V2(3)=(V1(3)-VORG(3))*SF
	V2(4)=1.0
	CALL MTV4(V1,VIEW,V2)	! xVN
	IF (DS.EQ.0.0.OR.V1(3).EQ.0.0) THEN
		CALL VCPY(LAST,V1)
	ELSE
		LAST(1)=V1(1)/V1(3)
		LAST(2)=V1(2)/V1(3)
		LAST(3)=1./V1(3)
		LAST(4)=V1(4)
	ENDIF
	IF (IABS(ID).GT.9) RETURN
	GOTO (5,10,20,30,40,50,50,50,50,20) IABS(ID)+1
 5	CONTINUE		! ID = 0
	CALL PLOT(X,Y,0)	! CHANGE COLOR
	IF (Z.GT.0.) DS=DS*Z
	RETURN
10	CONTINUE		! ID = 1
	IF (ID.GT.0) THEN	! CHANGE ROTATION MATRIX
		CALL ROTEM(1,X,TEMP1)
		CALL MATMUL4(ROT,ROT,TEMP1)
		CALL ROTEM(2,Y,TEMP1)	
		CALL MATMUL4(ROT,ROT,TEMP1)
		CALL ROTEM(3,Z,TEMP1)
		CALL MATMUL4(ROT,ROT,TEMP1)
	ELSE			! CHANGE SCALE FACTOR
		SF=X*SF
		IF (SF.LE.0.0) SF=1.0
	ENDIF
20	CONTINUE		! ID = 2,9
	IF (ID.LT.0) CALL VCPY(VORG,V2)
	CALL VCPY(V2,V0)
	CALL VCPY(V3,V1)
	IFLAG=IPCLP3(V2,V3) ! 3D PERSPECTIVE CLIPPING
	IF (IFLAG.NE.-1) THEN
	 IF (IZCLIP.EQ.1) THEN	! Z PLANE CLIPPING
	  IF (V2(3).GT.ZCLIP) THEN
	    IF (V1(3).GT.ZCLIP) GOTO 25
	    IF (V2(3)-V1(3).GT.0.0) THEN
		V2(1)=V1(1)+(ZCLIP-V1(3))*(V2(1)-V1(1))/(V2(3)-V1(3))
		V2(2)=V1(2)+(ZCLIP-V1(3))*(V2(2)-V1(2))/(V2(3)-V1(3))
	    ELSE	! ERROR HAS OCCURED
		GOTO 25
	    ENDIF
	   ELSE IF (V1(3).GT.ZCLIP) THEN
	     IF (V1(3)-V2(3).GT.0.0) THEN
		V1(1)=V2(1)+(ZCLIP-V2(3))*(V1(1)-V2(1))/(V1(3)-V2(3))
		V1(2)=V2(2)+(ZCLIP-V2(3))*(V1(2)-V2(2))/(V1(3)-V2(3))
	     ELSE	! ERROR HAS OCCURED
		GOTO 25
	     ENDIF
	   ENDIF
	 ENDIF
	 IF (I3D.NE.-1) CALL DRAW3D(V2,V3,IABS(ID),DS)
	ENDIF
25	CALL VCPY(V0,V1)
	RETURN
30	CONTINUE		! ID = 3
	IF (ID.LT.0) CALL VCPY(VORG,V2)
	CALL VCPY(V0,V1)
	RETURN
40	CONTINUE
	IF (ID.LT.0) THEN
		IZCLIP=0
		RETURN
	ELSE
		IZCLIP=1
		ZCLIP=V1(3)
	ENDIF
50	RETURN
	END
