C
C *** LAST REVISED ON  8-FEB-1994 00:03:54.86
C *** SOURCE FILE: [LONGLIB93.SOURCES.FORTRAN.MAPLIB]EARTH3D.FOR
C
C ***********************************************************************
C
C	SOURCE CODE FOR THE WORLD MAP ROUTINES OF THE LONGLIB GRAPHICS
C	LIBRARY.  THESE ROUTINES ACCESS A DATA FILE WITHIN THE LONGLIB
C	GRAPHICS DIRECTORY.  THIS FILE NAME AND LOCATION IS SPECIFIED
C	IN THE FILES.INC INCLUDE FILE WHICH SHOULD BE CUSTOMIZED
C	FOR EACH APPLICATION.
C
C ***********************************************************************
C
	SUBROUTINE EARTH3D(R,FLAT)
C
C	WRITTEN BY D. LONG   JULY 1985
C	DRAWS DATA FROM LAND-EDGE MAP OF THE EARTH IN 3d WITH THE OPTION
C	OF A SPHERICAL OR ELLIPSOIDAL EARTH
C
C	R	(R): EARTH RADIUS (IN PLOT UNITS)
C	FLAT	(R): EARTH FLATNESS
C			= 0 FOR SPHERICAL EARTH
C			= 3.3528132E-3 FOR ELLIPSOIDAL EARTH
C
C	READS THE DATA FILE EARTH.DAT IN THE LONGLIB GRAPHICS
C	DIRECTORY.  REQUIRES /home1/LongLib/aaData/ TO BE THIS DIRECTORY.
C	USES FORTRAN FILE UNIT NUMBER 2 FOR READING A FILE.
C
C	USES THE INIT3D 3D PACKAGE BY CALLING PLOT3D
C
	REAL DATIN(10),V(4)
	INCLUDE 'files.inc'
C
C	DEFINE DEGREE TO RADIAN CONVERSION
C
	DEGRAD(X)=X*3.141592654/180.0
C
C	OPEN FILE CONTAINING EARTH LOCATION DATA
C
	OPEN(UNIT=2,FILE=EARTH,FORM='FORMATTED',STATUS='OLD',ERR=9999)
C	1    ,READONLY)
C
C	BEGIN READING DATA
C
	ID=3
   10	READ(2,1005,END=999) DATIN
 1005	FORMAT(10F8.0)
C
	DO 20 I=1,10,2
		IF(IFIX(DATIN(I)).EQ.0.AND.
     $		   IFIX(DATIN(I+1)).EQ.0)GOTO 20
		IF(DATIN(I+1).EQ.888.8) THEN
C
C	END OF LINE SEGMENT
C
			ID=3
			GO TO 20
		ENDIF
		IF (ABS(DATIN(I)).GT.180.5.OR.
     $		    ABS(DATIN(I+1)).GT.91.) THEN
C	BAD DATA POINT
			IPT=3
			GO TO 20
		ENDIF
C
C	CONVERT INPUT LAT/LONG TO 3D VECTOR
C
		ALONG=DEGRAD(DATIN(I))
		ALAT=DEGRAD(DATIN(I+1))
C
C	COMPUTE EARTH RADIUS ASSUMING ELLIPSOIDAL EARTH
C
		RAD=R*(1.-FLAT*SIN(ALAT))
		ALAT=DEGRAD(DATIN(I+1)-90.)
		CALL SPRECT1(V,ALONG,ALAT,RAD)
		CALL PLOT3D(V(1),V(2),V(3),ID)
		ID=2
   20	CONTINUE
	GO TO 10
  999	CONTINUE
C
	CLOSE(2)
	RETURN
 9999	CONTINUE
	WRITE (*,199) EARTH
 199	FORMAT(' *** ERROR OPENING "',A,'" (LONGLOC:EARTH.DAT) FILE ***')
	RETURN
	END
