C
C *** LAST REVISED ON  8-FEB-1994 00:07:08.07
C *** SOURCE FILE: [LONGLIB93.SOURCES.FORTRAN.MAPLIB]LANDMAP.FOR
C ****************************************************************************
C
	SUBROUTINE LANDMAP(YLAT,XLONG,SLONG)
C
C	WRITTEN BY D. LONG JULY 1985  JPL
C
C	DRAWS A LAND-EDGE MAP USING DATA FROM VECTOR LAND MAP FILE
C	THIS ROUTINE USES A LINEAR PROJECTION, I.E. LAT/LONGS ARE ASSUMED
C	TO BE X,Y POINTS ON A PLANE.
C
C	YLAT	REAL	SCALE FACTOR LATITUDE INCH/360 DEGREES
C	XLONG 	REAL	SCALE FACTOR LONGITUTDE INC/180
C	SLONG	REAL	LEFT-MOST LONGITUDE (-180 TO +180)
C
C	USES FORTRAN FILE UNIT NUMBER 2.
C
C	NOTE: SINCE MAP DATA HAS ITS ORIGIN FROM -180 TO 180, IF WE WANT
C	THE MAP TO START FROM SOME OTHER POINT WE NEED TO DO SOME FANCY
C	TRICKS
C
	REAL DATIN(10)
	LOGICAL LAST,PENUP
	INCLUDE 'files.inc'
C
C	OPEN DATA FILE
C
	OPEN(UNIT=2,FILE=EARTH,FORM='FORMATTED',STATUS='OLD')
C       1  ,READONLY)
C
	IPT=3
	LAST=.FALSE.
	X=2.*XLONG
C
C	READ THE DATA
C
   10	READ(2,1005,END=999) DATIN
 1005	FORMAT(10F8.0)
C
C	CHECK THE STORED DATA COMMAND
C
	DO 20 I=1,10,2
		IF (IFIX(DATIN(I)).EQ.0.AND.
     $ 		    IFIX(DATIN(I+1)).EQ.0) GOTO 20
		IF (DATIN(I+1).EQ.888.8) THEN
C
C	 END OF LINE SEGMENT
C
			IPT=3
			CALL PLOT(X,Y,IPT)
			PENUP=.TRUE.
			LAST=.FALSE.
			X=XLONG*2.
			GO TO 20
		ENDIF
		IF (ABS(DATIN(I)).GT.180.5.OR.
     $		    ABS(DATIN(I+1)).GT.91.) THEN
C	BAD POINT
			IPT=3
			CALL PLOT(X,Y,IPT)
			PENUP=.TRUE.
			LAST=.FALSE.
			X=XLONG*2.
			GO TO 20
		ENDIF
		PENUP=.FALSE.
		IF (DATIN(I).LT.SLONG) THEN
C
C	LEFT OF DESIRED LONGITUDE
C
			IF (LAST) THEN
C
C	CLIP INTERSECTION LINE
C
			    X1=(DATIN(I)+360.-SLONG)/360.*XLONG
				Y1=(DATIN(I+1)+90.)/180.*YLAT
				Y2=Y1-(Y-Y1)*X1/(X-X1)
				CALL PLOT(X,Y2,IPT)
				CALL PLOT(X1,Y2,3)
				CALL PLOT(X1,Y1,2)
				X=X1
				Y=Y1
			ELSE
			    X=(DATIN(I)+360.-SLONG)/360.*XLONG
				Y=(DATIN(I+1)+90.)/180.*YLAT
				CALL PLOT(X,Y,IPT)
			ENDIF
			IPT=2
			LAST=.FALSE.
		ELSE
C
C	RIGHT OF DESIRED LONGITUDE
C
			IF (LAST) THEN
				X=(DATIN(I)-SLONG)/360.*XLONG
				Y=(DATIN(I+1)+90.)/180.*YLAT
				CALL PLOT(X,Y,IPT)
			ELSE
C
C	CLIP INTERSECTION LINE
C
				X1=(DATIN(I)-SLONG)/360.*XLONG
				Y1=(DATIN(I+1)+90.)/180.*YLAT
				Y2=Y-(Y1-Y)*X/(X1-X)
				CALL PLOT(X,Y2,IPT)
				CALL PLOT(X1,Y2,3)
				IPT=3
				IF (PENUP) IPT=3
				CALL PLOT(X1,Y1,IPT)
				X=X1
				Y=Y1
			ENDIF
			IPT=2
			LAST=.TRUE.
		ENDIF
   20	CONTINUE
	GO TO 10
  999	CONTINUE
	CLOSE(2)
C
	RETURN
	END
